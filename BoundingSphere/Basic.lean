/-
Copyright (c) 2025 Julien Michel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julien Michel
-/
import Mathlib

/-!
# Supremal extended distance to a set


## Main results


## Tags
metric space
-/

noncomputable section

open NNReal ENNReal Topology Set Filter Pointwise Bornology

universe u v

variable {Î± : Type u} {Î² : Type v}

namespace EMetric

section SupEdist

variable [PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] {x y : Î±} {s t : Set Î±} {Î¦ : Î± â†’ Î²}

/-! ### Supremal distance of a point to a set as a function into `â„â‰¥0âˆ`. -/

/-- The supremal edistance of a point to a set -/
noncomputable def supEdist (x : Î±) (s : Set Î±) : â„â‰¥0âˆ := â¨† y âˆˆ s, edist x y

@[simp]
theorem supEdist_empty : supEdist x âˆ… = 0 := iSup_emptyset

theorem supEdist_le {d} : supEdist x s â‰¤ d â†” âˆ€ y âˆˆ s, edist x y â‰¤ d := by
  simp only [supEdist, iSup_le_iff]

/-- The supEdist to a union is the maximum of the supEdist -/
@[simp]
theorem supEdist_union : supEdist x (s âˆª t) = supEdist x s âŠ” supEdist x t := iSup_union

@[simp]
theorem supEdist_iUnion (f : Î¹ â†’ Set Î±) (x : Î±) : supEdist x (â‹ƒ i, f i) = â¨† i, supEdist x (f i) :=
  iSup_iUnion f _

lemma supEdist_biUnion {Î¹ : Type*} (f : Î¹ â†’ Set Î±) (I : Set Î¹) (x : Î±) :
    supEdist x (â‹ƒ i âˆˆ I, f i) = â¨† i âˆˆ I, supEdist x (f i) := by simp only [supEdist_iUnion]

/-- The supEdist to a singleton is the edistance to the single point of this singleton -/
@[simp]
theorem supEdist_singleton : supEdist x {y} = edist x y := iSup_singleton

/-- The supEdist to a set is bounded below by the edist to any of its points -/
theorem edist_le_supEdist_of_mem (h : y âˆˆ s) : edist x y â‰¤ supEdist x s := by
  convert le_iSupâ‚‚ y h using 1
  rfl

/-- If a point `x` belongs to `s`, then its supEdist to `s` is less than or equal to the
diameter of `s` -/
theorem supEdist_le_diam_of_mem (h : x âˆˆ s) : supEdist x s â‰¤ diam s :=
  iSupâ‚‚_le fun _ hy => edist_le_diam_of_mem h hy

/-- The supEdist is monotone with respect to inclusion. -/
@[gcongr]
theorem supEdist_mono (h : s âŠ† t) : supEdist x s â‰¤ supEdist x t :=
  iSup_le_iSup_of_subset h

/-- The supEdist to a set is `> r` iff there exists a point in the set at edistance `> r` -/
theorem lt_supEdist_iff {r : â„â‰¥0âˆ} : r < supEdist x s â†” âˆƒ y âˆˆ s, r < edist x y := by
  simp_rw [supEdist, lt_iSup_iff, exists_prop]

/-- The supEdist of `x` to `s` is bounded by the sum of the supEdist of `y` to `s` and
the edist from `x` to `y` -/
theorem supEdist_le_supEdist_add_edist [Nonempty Î±] : supEdist x s â‰¤ supEdist y s + edist x y := by
  unfold supEdist
  rw [ENNReal.iSup_add]
  refine iSup_mono fun i â†¦ ?_
  obtain hi | hi := em' (i âˆˆ s)
  Â· simp [hi]
  have := Nonempty.intro hi
  rw [ENNReal.iSup_add]
  refine iSup_mono fun j â†¦ ?_
  rw [add_comm]
  apply edist_triangle

/-- The supEdist to a set depends continuously on the point -/
@[continuity, fun_prop]
theorem continuous_supEdist [Nonempty Î±] : Continuous fun x => supEdist x s :=
  continuous_of_le_add_edist 1 (by simp) <| by
    simp only [one_mul, supEdist_le_supEdist_add_edist, forallâ‚‚_true_iff]

/-- The supremum edistance is invariant under isometries -/
theorem supEdist_image (hÎ¦ : Isometry Î¦) : supEdist (Î¦ x) (Î¦ '' t) = supEdist x t := by
  simp only [supEdist, iSup_image, hÎ¦.edist_eq]

@[to_additive (attr := simp)]
theorem supEdist_smul {M} [SMul M Î±] [IsIsometricSMul M Î±] (c : M) (x : Î±) (s : Set Î±) :
    supEdist (c â€¢ x) (c â€¢ s) = supEdist x s :=
  supEdist_image (isometry_smul _ _)

theorem supEdist_eq_sSup : supEdist x s = sSup (edist x '' s) := sSup_image.symm

theorem supEdist_mem_of_isCompact (h1 : IsCompact s) (h2 : s.Nonempty) x :
    supEdist x s âˆˆ edist x '' s := by
  rw [supEdist_eq_sSup]
  apply IsCompact.sSup_mem
  Â· exact h1.image (continuous_const.edist continuous_id')
  Â· simp [h2]

theorem supEdist_mem_of_isFinite (h1 : s.Finite) (h2 : s.Nonempty) x :
    supEdist x s âˆˆ edist x '' s := supEdist_mem_of_isCompact h1.isCompact h2 _

theorem supEdist_ne_top_of_isBounded {Î±} [PseudoMetricSpace Î±] {s : Set Î±} (h1 : IsBounded s) x :
    supEdist x s â‰  âŠ¤ := by
  obtain h2 | h2 := s.eq_empty_or_nonempty
  Â· simp [h2]
  let t0 := h2.choose
  rw [Metric.isBounded_iff_ediam_ne_top] at h1
  apply ne_top_of_le_ne_top (add_ne_top.mpr âŸ¨h1, edist_ne_top t0 xâŸ©)
  rw [supEdist_eq_sSup, sSup_le_iff]
  intro _ âŸ¨t, ht1, ht2âŸ©
  subst ht2
  rw [edist_comm]
  apply le_trans (edist_triangle t t0 x)
  exact add_le_add_right (edist_le_diam_of_mem ht1 h2.choose_spec) (edist t0 x)

theorem supEdist_eq_top_of_not_isBounded {Î±} [PseudoMetricSpace Î±]
    {s : Set Î±} (h1 : Â¬IsBounded s) x : supEdist x s = âŠ¤ := by
  rw [supEdist_eq_sSup]
  contrapose! h1
  rw [Metric.isBounded_iff_ediam_ne_top, EMetric.diam_eq_sSup]
  contrapose! h1
  rw [sSup_eq_top] at h1 âŠ¢
  contrapose! h1
  obtain âŸ¨b, hb1, hb2âŸ© := h1
  replace hb2 : âˆ€ t âˆˆ s, edist x t â‰¤ b := by simpa using hb2
  use b + b, add_lt_top.mpr âŸ¨hb1, hb1âŸ©
  intro _ âŸ¨t, ht, r, hr, hxyâŸ©
  subst hxy
  apply le_trans (edist_triangle t x r)
  rw [edist_comm]
  exact add_le_add (hb2 t ht) (hb2 r hr)

end SupEdist

end EMetric



/-! Now, we turn to the same notions in metric spaces. To avoid the difficulties related to
`sInf` and `sSup` on `â„` (which is only conditionally complete), we use the notions in `â„â‰¥0âˆ`
formulated in terms of the edistance, and coerce them to `â„`.
Then their properties follow readily from the corresponding properties in `â„â‰¥0âˆ`,
modulo some tedious rewriting of inequalities from one to the other. -/

namespace Metric

section SupDist

variable [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] {s t : Set Î±} {x y : Î±} {Î¦ : Î± â†’ Î²}

open EMetric

/-! ### Supremal distance of a point to a set as a function into `â„`. -/

/-- The supremal distance of a point to a set -/
def supDist (x : Î±) (s : Set Î±) : â„ :=
  ENNReal.toReal (supEdist x s)

theorem supDist_eq_iSup : supDist x s = â¨† y : s, dist x y := by
  rw [supDist, supEdist, iSup_subtype', ENNReal.toReal_iSup]
  Â· simp only [dist_edist]
  Â· finiteness

/-- The supremal distance is always nonnegative -/
theorem supDist_nonneg : 0 â‰¤ supDist x s := toReal_nonneg

/-- The supremal distance to the empty set is 0 -/
@[simp]
theorem supDist_empty : supDist x âˆ… = 0 := by simp [supDist]

/-- The supremal distance to an unbounded set is `0`. -/
theorem supDist_eq_zero_of_not_isBounded (h1 : Â¬IsBounded s) : supDist x s = 0 := by
  simp [supDist, supEdist_eq_top_of_not_isBounded h1, toReal_top]

/-- The supremal distance to a bounded set coincides with the supremal edistance. -/
theorem supEdist_eq_supDist_of_isBounded (h1 : IsBounded s) x :
    supEdist x s = ENNReal.ofReal (supDist x s) := by
  rw [supDist, ofReal_toReal]
  exact supEdist_ne_top_of_isBounded h1 x

theorem supDist_le_diam_of_mem (hs : IsBounded s) (h : x âˆˆ s) : supDist x s â‰¤ diam s :=
  toReal_mono (isBounded_iff_ediam_ne_top.mp hs) (supEdist_le_diam_of_mem h)

/-- The supremal distance to a singleton is the distance to the unique point in this singleton. -/
@[simp]
theorem supDist_singleton : supDist x {y} = dist x y := by simp [supDist, dist_edist]

/-- The supremal distance to a set is â‰¥ to the distance to any point in this set. -/
theorem dist_le_supDist_of_mem (hs : IsBounded s) (h : y âˆˆ s) : dist x y â‰¤ supDist x s := by
  rw [dist_edist, supDist]
  exact toReal_mono (supEdist_ne_top_of_isBounded hs _) (edist_le_supEdist_of_mem h)

lemma isLUB_supDist (hs : s.Nonempty) (hs' : IsBounded s) :
    IsLUB ((dist x Â·) '' s) (supDist x s) := by
  simpa [supDist_eq_iSup, sSup_image']
    using isLUB_csSup (hs.image _) âŸ¨supDist x s, by
      simpa [upperBounds] using fun _ => dist_le_supDist_of_mem hs'âŸ©

/-- The supremal distance is monotone with respect to inclusion. -/
theorem supDist_mono (h : s âŠ† t) (ht : IsBounded t) : supDist x s â‰¤ supDist x t :=
  toReal_mono (supEdist_ne_top_of_isBounded ht _) (supEdist_mono h)

lemma supDist_le {r : â„} (hr : r â‰¥ 0) (hs : IsBounded s) :
    supDist x s â‰¤ r â†” âˆ€ y âˆˆ s, dist x y â‰¤ r := by
  rw [supDist, â†le_ofReal_iff_toReal_le (supEdist_ne_top_of_isBounded hs x) hr, supEdist_le]
  constructor <;> intro h y hy <;> specialize h y hy <;>
    simpa [dist_edist, le_ofReal_iff_toReal_le (edist_ne_top x y) hr] using h

/-- The supDist to a set is `> r` iff there exists a point in the set at distance `> r` -/
theorem lt_supDist_iff {r : â„} (hr : r â‰¥ 0) (hs : IsBounded s) :
    r < supDist x s â†” âˆƒ y âˆˆ s, r < dist x y := by
  simpa using not_congr (supDist_le hr hs)

/-- The supDist of `x` to `s` is bounded by the sum of the supDist of `y` to `s` and
the distance from `x` to `y` -/
theorem supDist_le_supDist_add_dist [Nonempty Î±] : supDist x s â‰¤ supDist y s + dist x y := by
  by_cases hs : IsBounded s
  Â· unfold supDist
    rw [dist_edist, â†toReal_add (supEdist_ne_top_of_isBounded hs y) (edist_ne_top x y)]
    apply toReal_mono
    Â· exact add_ne_top.mpr âŸ¨supEdist_ne_top_of_isBounded hs y, edist_ne_top x yâŸ©
    Â· exact supEdist_le_supEdist_add_edist
  Â· simp [supDist_eq_zero_of_not_isBounded hs]

/-- The supremal distance to a set is Lipschitz in point with constant 1 -/
theorem lipschitz_supDist_pt [Nonempty Î±] (s : Set Î±) : LipschitzWith 1 (supDist Â· s) :=
  LipschitzWith.of_le_add fun _ _ => supDist_le_supDist_add_dist

/-- The supremal distance to a set is uniformly continuous in point -/
theorem uniformContinuous_supDist_pt [Nonempty Î±] (s : Set Î±) : UniformContinuous (supDist Â· s) :=
  (lipschitz_supDist_pt s).uniformContinuous

/-- The minimal distance to a set is continuous in point -/
@[continuity, fun_prop]
theorem continuous_supDist_pt [Nonempty Î±] (s : Set Î±) : Continuous (supDist Â· s) :=
  (uniformContinuous_supDist_pt s).continuous

/-- The supremum distance is invariant under isometries. -/
theorem supDist_image (hÎ¦ : Isometry Î¦) : supDist (Î¦ x) (Î¦ '' t) = supDist x t := by
  simp [supDist, supEdist_image hÎ¦]

@[to_additive (attr := simp)]
theorem supDist_smul {M} [SMul M Î±] [IsIsometricSMul M Î±] (c : M) (x : Î±) (s : Set Î±) :
    supDist (c â€¢ x) (c â€¢ s) = supDist x s :=
  supDist_image (isometry_smul _ _)

theorem supDist_eq_sSup x : supDist x s = sSup (dist x '' s) := by
  rw [supDist, supEdist_eq_sSup, toReal_sSup]
  Â· congr 1
    ext x
    simp [edist_dist, dist_nonneg, toReal_ofReal]
  Â· simp [edist_ne_top]

theorem supDist_mem_of_isCompact (h1 : IsCompact s) (h2 : s.Nonempty) x :
    supDist x s âˆˆ dist x '' s := by
  rw [supDist_eq_sSup]
  apply IsCompact.sSup_mem
  Â· exact h1.image (continuous_const.dist continuous_id')
  Â· simp [h2]

theorem supDist_mem_of_isFinite (h1 : s.Finite) (h2 : s.Nonempty) x :
    supDist x s âˆˆ dist x '' s := supDist_mem_of_isCompact h1.isCompact h2 _

end SupDist

end Metric

end




/-
Copyright (c) 2025 Julien Michel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julien Michel
-/

/-!
# Minimal bounding sphere

In this file we develop a basic theory of the minimal bounding sphere in a finite dimensional
euclidean affine space.
In such a space, the minimal bounding sphere of a nonempty bounded set exists and is unique.
Most results are about the radius and center of the sphere, rather than the sphere itself.

## Main definitions

- `BoundingSphere.radius`: The radius of the minimal bounding sphere.
- `BoundingSphere.center`: The center of the minimal bounding sphere.

## Main results

- `BoundingSphere.radius_mem_range`: Key lemma used to define the center.
- `BoundingSphere.radius_le`: The radius of the minimal bounding sphere is less than or equal to
  that of any other ball containing the set.
- `BoundingSphere.subset`: The minimal bounding sphere contains the set.
- `BoundingSphere.radius_eq_radius_of_IsMinimal` and
  `BoundingSphere.center_eq_center_of_IsMinimal`: Uniqueness of the minimal bounding sphere.

-/

namespace BoundingSphere

open Bornology ENNReal Metric EMetric InnerProductSpace Pointwise

variable {V} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
variable {P} [MetricSpace P] [NormedAddTorsor V P] {s t : Set P}

/-- The radius of the minimal bounding sphere of a set, defined as the infimum of the supremal
distance from a point to the set. -/
noncomputable def radius
    {V} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
    {P} [MetricSpace P] [NormedAddTorsor V P] (s : Set P) :=
  (â¨… x, supEdist x s).toReal

/-- The radius of the minimal bounding sphere is non negative. -/
theorem radius_nonneg : radius s â‰¥ 0 := by simp [radius]

/-- The radius of the minimal bounding sphere of the empty set is `0`. -/
@[simp]
theorem radius_empty : radius (âˆ… : Set V) = 0 := by simp [radius]

theorem radius_eq_zero_of_not_isBounded (h1 : Â¬IsBounded s) : radius s = 0 := by
  simp [radius, EMetric.supEdist_eq_top_of_not_isBounded h1]

/-- The radius of the minimal bounding sphere of a bounded set `s` is less than or equal to
that of any other ball containing `s`. -/
theorem radius_le (h0 : s.Nonempty) (h1 : IsBounded s) :
    âˆ€ c', âˆ€ r', s âŠ† Metric.closedBall c' r' â†’ radius s â‰¤ r' := by
  intro c' r' h2
  have hr' := calc
    r' â‰¥ dist h0.choose c' := h2 h0.choose_spec
    _ â‰¥ 0 := dist_nonneg
  unfold radius
  rw [â†le_ofReal_iff_toReal_le _ hr', iInf_le_iff]
  Â· intro x hx
    specialize hx c'
    rw [supEdist, le_iSup_iff] at hx
    apply hx
    intro y
    rw [iSup_le_iff]
    intro hy
    rw [edist_le_ofReal hr', dist_comm]
    exact h2 hy
  Â· simp [EMetric.supEdist_ne_top_of_isBounded h1]

/-- The radius of the minimal bounding sphere of a singleton is `0`. -/
@[simp]
theorem radius_singleton (a : V) : radius {a} = 0 := by
  have := radius_le (Set.singleton_nonempty a) isBounded_singleton a 0 (by simp)
  exact le_antisymm this radius_nonneg

/-- Translating a set does not change the radius of
its minimal bounding sphere. -/
@[simp]
theorem radius_vadd (s : Set P) (v : V) : radius (v +áµ¥ s) = radius s := by
  unfold radius
  rw [(AffineIsometryEquiv.constVAdd â„ P (-v)).toEquiv.iInf_congr]
  simpa using fun x => EMetric.supEdist_vadd (-v) x (v +áµ¥ s)

/-- The radius of the minimal bounding sphere is attained as a supremal distance
from some point to the set. -/
theorem radius_mem_range (s : Set P) : radius s âˆˆ Set.range (supDist Â· s) := by
  obtain h0 | h0 := s.eq_empty_or_nonempty
  Â· simp [radius, h0]
  obtain h1 | h1 := em' (IsBounded s)
  Â· simp [radius_eq_zero_of_not_isBounded h1, supDist_eq_zero_of_not_isBounded h1]
  unfold radius
  suffices â¨… x, supEdist x s âˆˆ Set.range (supEdist Â· s) by
    simp only [Set.mem_range] at this
    simp only [supDist, Set.mem_range]
    obtain âŸ¨y, hyâŸ© := this
    use y
    congr 1
  let s0 := h0.choose
  have hs0 : s0 âˆˆ s := h0.choose_spec
  let K := EMetric.closedBall s0 (2 * supEdist s0 s)
  suffices â¨… x âˆˆ K, supEdist x s âˆˆ (supEdist Â· s) '' K by
    apply Set.mem_range_of_mem_image _ K
    convert this using 1
    apply csInf_eq_csInf_of_forall_exists_le
    Â· intro _ âŸ¨c, hcâŸ©
      subst hc
      by_cases hc2 : c âˆˆ K
      Â· use supEdist c s
        exact âŸ¨by use c; exact (iInf_pos hc2), by simpâŸ©
      Â· use supEdist s0 s
        split_ands
        Â· use s0, by simp [K]
        Â· calc
            supEdist s0 s â‰¤ supEdist s0 s + supEdist s0 s := le_add_self
            _ = 2 * supEdist s0 s := by rw [two_mul]
            _ â‰¤ edist c s0 := le_of_lt (by simpa [K] using hc2)
            _ â‰¤ _ := edist_le_supEdist_of_mem hs0
    Â· intro _ âŸ¨y, hyâŸ©
      subst hy
      use supEdist y s
      simp
  have hK : IsCompact K := by
    unfold K
    let f := (AffineIsometryEquiv.constVSub â„ s0).symm.toIsometryEquiv
    let K' := Metric.closedBall (0 : V) (2 * supDist s0 s)
    convert_to IsCompact (f '' K') using 1
    Â· rw [f.image_closedBall, â†emetric_closedBall]
      Â· congr 1
        Â· simp [f]
        Â· simp [supEdist_eq_supDist_of_isBounded h1]
      Â· simp [supDist_nonneg]
    exact (isCompact_closedBall _ _).image_of_continuousOn f.continuous.continuousOn
  rw [â†sInf_image]
  apply IsCompact.sInf_mem
  Â· exact hK.image_of_continuousOn continuous_supEdist.continuousOn
  Â· use supEdist s0 s, s0, by simp [K]

open Classical in
/-- The center of the minimal bounding sphere of a non empty bounded set -/
noncomputable def center
    {V} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
    {P} [MetricSpace P] [NormedAddTorsor V P] (s : Set P) :=
  (radius_mem_range s).choose

/-- The radius of the minimal bounding sphere of a set is the supremal distance
from its center to the set. -/
theorem radius_eq_supDist_center : radius s = supDist (center s) s :=
  (radius_mem_range s).choose_spec.symm

/-- The minimal bouding ball of a bounded set contains it. -/
theorem subset (h1 : IsBounded s) : s âŠ† Metric.closedBall (center s) (radius s) := by
  by_cases h0 : s.Nonempty
  Â· intro p hp
    rw [Metric.mem_closedBall, radius_eq_supDist_center, dist_comm]
    exact dist_le_supDist_of_mem h1 hp
  Â· simp [Set.not_nonempty_iff_eq_empty.mp h0]

/-- A set `s` is minimally enclosed by a closed ball with center `c` and radius `r`
if `s` is contained in the closed ball and any closed ball containing `s` has radius at least
`r`. -/
def IsMinimal {Î±} [PseudoMetricSpace Î±] (s : Set Î±) c r :=
  s âŠ† Metric.closedBall c r âˆ§ âˆ€ c', âˆ€ r', s âŠ† Metric.closedBall c' r' â†’ r â‰¤ r'

theorem IsMinimal.of_isBounded (h0 : s.Nonempty) (h1 : IsBounded s) :
    IsMinimal s (center s) (radius s) := âŸ¨subset h1, radius_le h0 h1âŸ©

/-- The radius of a minimal bounding sphere is unique. -/
theorem radius_eq_radius_of_IsMinimal [PseudoMetricSpace Î±] {s : Set Î±} {x r1 y r2}
    (h1 : IsMinimal s x r1) (h2 : IsMinimal s y r2) : r1 = r2 :=
  le_antisymm (h1.right y r2 h2.left) (h2.right x r1 h1.left)

/-- The center of a minimal bounding sphere is unique.
Thus the minimal bounding sphere is unique. -/
theorem center_eq_center_of_IsMinimal
    {V} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
    {P} [MetricSpace P] [NormedAddTorsor V P] {s : Set P}
    (h0 : s.Nonempty) {x y : P} {r1 r2 : â„}
    (h1 : IsMinimal s x r1) (h2 : IsMinimal s y r2) : x = y := by
  have := radius_eq_radius_of_IsMinimal h1 h2
  subst this
  let s0 := h0.choose
  have hs0 : s0 âˆˆ s := h0.choose_spec
  have hr1 := calc
      r1 â‰¥ dist s0 y := h2.left hs0
      _ â‰¥ 0 := dist_nonneg
  let r0 := dist x y / 2
  let c := midpoint â„ x y
  set B1 := closedBall x r1
  set B2 := closedBall y r1
  have h3 z (hz1 : z âˆˆ B1) (hz2 : z âˆˆ B2) : dist z c ^ 2 â‰¤ r1 ^ 2 - r0 ^ 2 :=
    let a := x -áµ¥ z
    let b := y -áµ¥ z
    calc
    dist z c ^ 2 = â€–c -áµ¥ zâ€– ^ 2 := by rw [dist_comm, dist_eq_norm_vsub]
    _ = (1 / 4 : â„) * â€–a + bâ€– ^ 2  := by
      unfold a b
      rw [midpoint_vsub, â†smul_add, norm_smul, mul_pow]
      norm_num
    _ = (1 / 4 : â„) * (2 * â€–aâ€– ^ 2 + 2 * â€–bâ€– ^ 2 - â€–a - bâ€– ^ 2) := by
      rw [norm_add_sq_real a b, norm_sub_sq_real a b]
      ring
    _ = (1 / 4 : â„) * (2 * â€–x -áµ¥ zâ€– ^ 2 + 2 * â€–y -áµ¥ zâ€– ^ 2 - â€–y -áµ¥ xâ€– ^ 2) := by
      congr 3
      rw [norm_sub_rev]
      simp [a, b]
    _ = (1 / 2 : â„) * â€–x -áµ¥ zâ€– ^ 2 + (1 / 2 : â„) * â€–y -áµ¥ zâ€– ^ 2 - (1 / 4 : â„) * â€–y -áµ¥ xâ€– ^ 2 := by
      ring
    _ â‰¤ (1 / 2 : â„) * r1 ^ 2 + (1 / 2 : â„) * r1 ^ 2 - (1 / 4 : â„) * (2 * r0) ^ 2 := by
      gcongr 4
      Â· simpa [B1, dist_comm, dist_eq_norm_vsub] using hz1
      Â· simpa [B2, dist_comm, dist_eq_norm_vsub] using hz2
      Â· apply le_of_eq
        rw [â†dist_eq_norm_vsub, dist_comm]
        ring
    _ = r1 ^ 2 - r0 ^ 2 := by ring
  have h4 : s âŠ† closedBall c âˆš(r1 ^ 2 - r0 ^ 2) := by
    intro z hz
    rw [Metric.mem_closedBall]
    calc
      _ = âˆš(dist z c ^ 2) := by
        symm
        apply Real.sqrt_sq
        apply dist_nonneg
      _ â‰¤ âˆš(r1 ^ 2 - r0 ^ 2) := Real.sqrt_le_sqrt (h3 z (h1.left hz) (h2.left hz))
  have := h1.right c (âˆš(r1 ^ 2 - r0 ^ 2)) h4
  replace := calc
    r1 ^ 2 â‰¤ âˆš(r1 ^ 2 - r0 ^ 2) ^ 2 := by gcongr 1
    _ = r1 ^ 2 - r0 ^ 2 := by
      apply Real.sq_sqrt
      calc
        0 â‰¤ dist s0 c ^ 2 := by apply sq_nonneg
        _ â‰¤ _ := h3 s0 (h1.left hs0) (h2.left hs0)
  replace : r0 = 0 := by nlinarith only [this]
  unfold r0 at this
  replace : dist x y = 0 := by linarith only [this]
  simpa [dist_eq_zero] using this

/-- Translating a set translates the center of its minimal bounding sphere accordingly. -/
theorem center_vadd (h1 : s.Nonempty) (h2 : IsBounded s) (v : V) :
    center (v +áµ¥ s) = v +áµ¥ center s := by
  have h1' : (v +áµ¥ s).Nonempty := h1.image _
  have h2' : IsBounded (v +áµ¥ s) := h2.vadd v
  have h3 := IsMinimal.of_isBounded h1' h2'
  have h4 : IsMinimal (v +áµ¥ s) (v +áµ¥ center s) (radius s) := by
    split_ands
    Â· rw [â†Metric.vadd_closedBall]
      exact Set.vadd_set_mono (subset h2)
    Â· intro c' r' h
      simpa using radius_le h1' h2' c' _ h
  exact center_eq_center_of_IsMinimal h1' h3 h4

/-- The radius of the minimal bounding sphere of a bounded set with at least two points
is strictly positive. -/
theorem radius_pos (h1 : IsBounded s) (h2 : s.encard â‰¥ 2) : radius s > 0 := by
  obtain âŸ¨x0, hx0, x1, hx1, h3âŸ© : âˆƒ x0 âˆˆ s, âˆƒ x1 âˆˆ s, x0 â‰  x1 := by
    have f : Fin 2 â†ª s := by
      by_cases h3 : s.Finite
      Â· have := h3.fintype
        let a : Fin (Fintype.card s) â†ª s := this.equivFin.symm.toEmbedding
        let b : Fin 2 â†ª Fin (Fintype.card s) :=
          Fin.castLEEmb (by apply ENat.coe_le_coe.mp; simp [h2])
        exact b.trans a
      Â· let a : â„• â†ª s := Set.Infinite.natEmbedding s h3
        let b : Fin 2 â†ª â„• := Fin.valEmbedding
        exact b.trans a
    let x0 := f âŸ¨0, by simpâŸ©
    let x1 := f âŸ¨1, by simpâŸ©
    use x0.1, x0.2, x1.1, x1.2
    rw [Subtype.coe_inj.ne]
    apply f.injective.ne
    simp
  set r := radius s
  set c := center s
  calc
    r = (r + r) / 2 := by ring
    _ â‰¥ (dist x0 c + dist c x1) / 2 := by
      gcongr 2
      Â· simpa using subset h1 hx0
      Â· simpa [dist_comm] using subset h1 hx1
    _ â‰¥ dist x0 x1 / 2 := by gcongr 1; apply dist_triangle
    _ > 0 / 2 := by gcongr 1; exact dist_pos.mpr h3
    _ = 0 := by simp

/-- The minimal bounding sphere of a finite set hits some point of the set. -/
theorem nonempty_sphere_of_finite (h1 : s.Finite) (h2 : s.Nonempty) :
    (s âˆ© sphere (center s) (radius s)).Nonempty := by
  have hc := subset h1.isBounded
  set c := center s
  set r := radius s
  obtain âŸ¨y0, hy0, hy0'âŸ© := supDist_mem_of_isFinite h1 h2 c
  rw [dist_comm] at hy0'
  set r' := supDist c s
  have h3 : r â‰¤ r' := by
    apply radius_le h2 h1.isBounded c r'
    intro z hz
    simpa [dist_comm] using dist_le_supDist_of_mem h1.isBounded hz
  have h4 : r' â‰¤ r := by simpa [hy0'] using hc hy0
  replace h2 : r = r' := le_antisymm h3 h4
  have h5 : y0 âˆˆ s âˆ© sphere c r := by simp [sphere, hy0, hy0', h2]
  use y0

end BoundingSphere




/-
Copyright (c) 2025 Julien Michel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julien Michel
-/

/-!
# Upper bounds on the radius of the minimal bounding sphere

In this file we prove some upper bounds on the radius of the minimal bounding sphere
of a nonempty bounded set in a proper inner product space.

## Main results

- `BoundingSphere.center_mem_convexHull_sphere_of_finite`:
  The center of the minimal bounding sphere of a non empty finite set `X`
  is contained in the convex hull of the points of `X` that lie on the sphere.
- `BoundingSphere.radius_le_sqrt_of_finite`:
  An upper bound on the radius of the minimal bounding sphere of a finite set.
- `BoundingSphere.radius_le_sqrt_of_isBounded`:
  An upper bound on the radius of the minimal bounding sphere of a bounded set.
  This result was originally proved by H. Jung in 1901.
-/

namespace BoundingSphere

open Bornology ENNReal Metric InnerProductSpace Pointwise Finset Module

variable {V} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
variable {X : Set V}

/-- The center of the minimal bounding sphere of a non empty finite set `s`
is contained in the convex hull of the points of `s` that lie on the sphere. -/
theorem center_mem_convexHull_sphere_of_finite (hX1 : X.Finite) (hX2 : X.Nonempty) :
    center X âˆˆ convexHull â„ (X âˆ© sphere (center X) (radius X)) := by
  set c := center X
  set r := radius X
  -- Denote `Y` the points of `X` that lie on the sphere
  set Y := X âˆ© sphere c r
  have hX3 := hX1.fintype
  have hY1 := Fintype.ofFinite Y
  have hY2 : Y âŠ† X := by simp [Y]
  -- By contradiction, assume that the center `c` is not in the convex hull of `Y`
  by_contra! h1
  -- There exists a vector `v` separating `c` from the convex hull of `Y`
  obtain âŸ¨v, hv, h2âŸ© : âˆƒ v, v â‰  0 âˆ§ âˆ€ x âˆˆ convexHull â„ Y, âŸªv, x - câŸ«_â„ > 0 := by
    set s : Set V := {0}
    have hs1 : Convex â„ s := convex_singleton _
    have hs2 : IsCompact s := isCompact_singleton
    set t := (Â· - c) '' convexHull â„ Y
    have ht1 : Convex â„ t := by
      let f := AffineMap.id â„ _ - AffineMap.const â„ _ c
      apply Convex.affine_image f
      apply convex_convexHull
    have ht2 : IsCompact t := IsCompact.image
      (hX1.subset hY2).isCompact_convexHull (continuous_sub_right c)
    have ht3 : IsClosed t := IsCompact.isClosed ht2
    have ht4 : Y.Nonempty := nonempty_sphere_of_finite hX1 hX2
    have ht5 : t.Nonempty := Set.image_nonempty.mpr ht4.convexHull
    have hst : Disjoint s t := by
      simp [s, t]
      intro x hx
      contrapose! h1
      convert hx using 1
      apply_fun (Â· + c) at h1
      simpa using h1.symm
    obtain âŸ¨f, u, w, hu, huw, hwâŸ© := geometric_hahn_banach_compact_closed hs1 hs2 ht1 ht3 hst
    replace hu : u > 0 := by simpa [s] using hu
    let v := (InnerProductSpace.toDual â„ V).symm f
    have hf x : f x = âŸªv, xâŸ«_â„ := by simp [v]
    refine âŸ¨v, ?_, ?_âŸ©
    Â· by_contra! hv
      specialize hw ht5.choose ht5.choose_spec
      simp [hf, hv] at hw
      linarith only [hu, huw, hw]
    Â· intro x hx
      specialize hw (x - c) (by simp [t, hx])
      simp [hf] at hw
      linarith only [hu, huw, hw]
  -- Perturb the center `c` a bit in the direction of `v`
  let c' (Îµ : â„) := c + Îµ â€¢ v
  -- For a small enough perturbation, all points of `Y` are in the interior of the ball
  obtain âŸ¨Î´Y, hÎ´Y, hcYâŸ© : âˆƒ Î´Y > 0, âˆ€ Îµ, Îµ > 0 â†’ Îµ < Î´Y â†’ âˆ€ x âˆˆ Y, â€–x - c' Îµâ€– ^ 2 < r ^ 2 := by
    let Î´ x := âŸªv, x - câŸ«_â„
    have hY3 : Y.toFinset.Nonempty := Set.toFinset_nonempty.mpr (nonempty_sphere_of_finite hX1 hX2)
    let d := Y.toFinset.inf' hY3 Î´
    have hd1 xi (hxi : xi âˆˆ Y) : d â‰¤ Î´ xi := Y.toFinset.inf'_le Î´ (by simpa using hxi)
    have hd2 : âˆƒ xi âˆˆ Y, Î´ xi = d := by
      convert Y.toFinset.exists_mem_eq_inf' hY3 Î´ using 2 with xi; simp [d]; tauto
    have hd3 : d > 0 := by
      obtain âŸ¨x0, hx0, hdâŸ© := hd2
      rw [â†hd]
      apply h2 x0
      exact mem_convexHull_iff.mpr fun _ a _ => a hx0
    use 2 * d / â€–vâ€– ^ 2, by field_simp; nlinarith only [hd3]
    intro Îµ hÎµ1 hÎµ2 xi hxi
    calc
      â€–xi - c' Îµâ€– ^ 2 = â€–(xi - c) - Îµ â€¢ vâ€– ^ 2 := by congr 2; module
      _ = â€–xi - câ€– ^ 2 - 2 * Îµ * âŸªv, xi - câŸ«_â„ + â€–Îµ â€¢ vâ€– ^ 2 := by
        rw [norm_sub_sq_real, real_inner_comm, real_inner_smul_left]
        ring
      _ = â€–xi - câ€– ^ 2 - 2 * Îµ * âŸªv, xi - câŸ«_â„ + Îµ ^ 2 * â€–vâ€– ^ 2 := by
        congr 1
        rw [norm_smul, mul_pow, Real.norm_of_nonneg]
        exact hÎµ1.le
      _ â‰¤ â€–xi - câ€– ^ 2 - 2 * Îµ * d + Îµ ^ 2 * â€–vâ€– ^ 2 := by gcongr 3; exact hd1 xi hxi
      _ = â€–xi - câ€– ^ 2 + (-2 * Îµ * d + Îµ ^ 2 * â€–vâ€– ^ 2) := by ring
      _ < â€–xi - câ€– ^ 2 + 0 := by
        gcongr 1
        calc
          -2 * Îµ * d + Îµ ^ 2 * â€–vâ€– ^ 2 = Îµ * (-2 * d + Îµ * â€–vâ€– ^ 2) := by ring
          _ < Îµ * 0 := by
            gcongr 1
            calc
              _ < -2 * d + (2 * d / â€–vâ€– ^ 2) * â€–vâ€– ^ 2 := by gcongr 2
              _ = -2 * d + 2 * d := by congr 1; field_simp
              _ = _ := by ring
          _ = _ := by ring
      _ = _ := by simp [Y] at hxi; simp [hxi]
  -- For a small enough perturbation, all points of `X \ Y` are also in the interior of the ball
  let Z := X \ Y
  obtain âŸ¨Î´Z, hÎ´Z, hcZâŸ© : âˆƒ Î´Z > 0, âˆ€ Îµ, Îµ > 0 â†’ Îµ < Î´Z â†’ âˆ€ x âˆˆ Z, â€–x - c' Îµâ€– ^ 2 < r ^ 2 := by
    have hZ0 := Fintype.ofFinite Z
    by_cases hZ1 : Z = âˆ…
    Â· simp [hZ1]; use 1; norm_num
    replace hZ1 := Set.toFinset_nonempty.mpr (Set.nonempty_iff_ne_empty.mpr hZ1)
    let f Îµ := Z.toFinset.sup' hZ1 (fun x => â€–x - c' Îµâ€– ^ 2)
    have hf : Continuous f := by apply Continuous.finset_sup'_apply; fun_prop
    replace hf : ContinuousAt f 0 := by apply hf.continuousAt
    rw [Metric.continuousAt_iff] at hf
    have f0_lt : f 0 < r ^ 2 := by
      unfold f
      rw [Finset.sup'_lt_iff]
      intro x hx
      suffices dist x c ^ 2 < r ^ 2 by simpa [c', â†dist_eq_norm] using this
      rw [sq_lt_sqâ‚€]
      Â· simp [Z] at hx
        apply lt_of_le_of_ne
        Â· exact subset hX1.isBounded hx.left
        Â· have := hx.right
          contrapose! this
          simp [Y, hx.left, â†dist_eq_norm, this]
      Â· apply dist_nonneg
      Â· apply radius_nonneg
    replace âŸ¨Î´, hÎ´, hfâŸ© := hf (r ^ 2 - f 0) (by linarith only [f0_lt])
    use Î´, hÎ´
    intro Îµ hÎµ1 hÎµ2
    simp only [dist_eq_norm] at hf
    have hÎµÎ´ : â€–Îµ - 0â€– < Î´ := by
      rw [Real.norm_of_nonneg]
      Â· linarith only [hÎµ2]
      Â· linarith only [hÎµ1]
    specialize hf hÎµÎ´
    intro x hx
    calc
      _ â‰¤ f Îµ := by
        unfold f
        rw [Finset.le_sup'_iff]
        use x, by simpa using hx
      _ = (f Îµ - f 0) + f 0 := by ring
      _ â‰¤ â€–f Îµ - f 0â€– + f 0 := by gcongr 1; apply Real.le_norm_self
      _ < r ^ 2 := by linarith only [hf]
  -- Thus perturbing the center by a small amout yields a smaller ball still enclosing all of `X`,
  obtain âŸ¨Î´X, hÎ´X, hcXâŸ© : âˆƒ Î´X > 0, âˆ€ Îµ, Îµ > 0 â†’ Îµ < Î´X â†’ âˆ€ x âˆˆ X, â€–x - c' Îµâ€– ^ 2 < r ^ 2 := by
    use Î´Y âŠ“ Î´Z, lt_min hÎ´Y hÎ´Z
    intro Îµ hÎµ1 hÎµ2 x hx
    obtain h | h : x âˆˆ Y âˆ¨ x âˆˆ Z := by simp [Y, Z, hx]; tauto
    Â· exact hcY Îµ hÎµ1 (lt_of_lt_of_le hÎµ2 inf_le_left) x h
    Â· exact hcZ Îµ hÎµ1 (lt_of_lt_of_le hÎµ2 inf_le_right) x h
  -- Contradicting the minimality of the original ball.
  let Î´0 := Î´X / 2
  obtain âŸ¨x, hx, hr0âŸ© := X.toFinset.exists_mem_eq_sup' (Set.toFinset_nonempty.mpr hX2) (â€–Â· - c' Î´0â€–)
  set r0 := X.toFinset.sup' (Set.toFinset_nonempty.mpr hX2) (â€–Â· - c' Î´0â€–)
  have h3 : X âŠ† closedBall (c' Î´0) r0 := by
    intro x hx
    simp only [Metric.mem_closedBall, dist_eq_norm, r0]
    rw [Finset.le_sup'_iff]
    use x, by simpa using hx
  have h4 : r â‰¤ r0 := radius_le hX2 hX1.isBounded (c' Î´0) r0 h3
  have h5 := calc
    r0 = âˆš(r0 ^ 2) := by
      rw [Real.sqrt_sq]
      rw [Finset.le_sup'_iff]
      use hX2.choose, by simpa using hX2.choose_spec
      apply norm_nonneg
    _ < âˆš(r ^ 2) := by
      apply Real.sqrt_lt_sqrt
      Â· apply sq_nonneg
      rw [hr0]
      apply hcX Î´0
      Â· unfold Î´0; linarith only [hÎ´X]
      Â· unfold Î´0; linarith only [hÎ´X]
      Â· simpa using hx
    _ = r := by
      rw [Real.sqrt_sq]
      apply radius_nonneg
  linarith only [h4, h5]

/-- A finite set with at least two points has at least two points on the boundary
of its minimal bounding sphere. -/
theorem encard_sphere_ge_two_of_finite (hX1 : X.encard â‰¥ 2) (hX2 : X.Finite) :
    (X âˆ© sphere (center X) (radius X)).encard â‰¥ 2 := by
  have hX3 := hX2.isBounded
  have hX4 : X.Nonempty := by
    apply Set.encard_ne_zero.mp
    by_contra! h0
    simp [h0] at hX1
  set c := center X
  set r := radius X
  set Y := X âˆ© sphere c r
  obtain hY1 | hY1 : Â¬Y.Finite âˆ¨ Y.Finite := by tauto
  Â· rw [Set.encard_eq_top]
    Â· simp
    Â· simpa using hY1
  obtain hY2 | hY2 | hY2 : Y.encard = 0 âˆ¨ Y.encard = 1 âˆ¨ Y.encard â‰¥ 2 := by
    have := hY1.fintype
    unfold Set.encard
    rw [ENat.card_eq_coe_natCard]
    norm_cast
    omega
  Â· exfalso
    rw [Set.encard_eq_zero] at hY2
    have hY3 := nonempty_sphere_of_finite hX2 hX4
    contrapose! hY3
    exact hY2
  Â· exfalso
    rw [Set.encard_eq_one] at hY2
    obtain âŸ¨x, hxâŸ© := hY2
    have hx1 : x âˆˆ Y := by simp [hx]
    have hx2 : x âˆˆ X := hx1.left
    have hx3 := hx1.right
    have h1 : c âˆˆ convexHull â„ Y := center_mem_convexHull_sphere_of_finite hX2 hX4
    replace h1 : c = x := by simpa [hx] using h1
    have h2 : r = 0 := by simpa [sphere, c, h1] using hx3.symm
    have h3 : r > 0 := radius_pos hX3 hX1
    linarith only [h2, h3]
  Â· exact hY2

/-- An upper bound on the radius of the minimal bounding sphere of a finite set. -/
theorem radius_le_sqrt_of_finite [DecidableEq V] {d : â„•} (hX1 : X.Finite) (hXd : X.ncard â‰¤ d + 1) :
    radius X â‰¤ âˆš(d / (2 * d + 2) : â„) * diam X := by
  -- Handle cases where `X` has 0 or 1 point first to avoid later divisions by a diameter of zero.
  obtain hX2 | hX2 | hX2 : X.ncard = 0 âˆ¨ X.ncard = 1 âˆ¨ X.ncard â‰¥ 2 := by omega
  Â· rw [Set.ncard_eq_zero hX1] at hX2
    simp [hX2]
  Â· have âŸ¨a, haâŸ© := Set.ncard_eq_one.mp hX2
    simp [ha, radius_singleton]
  have hX3 : X.Nonempty := by by_contra! h; simp [h] at hX2
  -- Without loss of generality, translate `X` so that its center is at the origin.
  wlog hc : center X = 0
  Â· let T := -center X +áµ¥ X
    have hT : T.ncard = X.ncard := Set.ncard_image_of_injective _ (add_right_injective _)
    specialize this (X := T) (d := d)
    specialize this hX1.vadd_set
    specialize this (by simpa [hT] using hXd)
    specialize this (by simpa [hT] using hX2)
    specialize this (by simpa [T] using hX3)
    specialize this (by simp [T, center_vadd hX3 hX1.isBounded (-center X)])
    convert this using 1 <;> simp [T]
  have hX0 := hX1.fintype
  have hX4 : diam X > 0 := by
    let a : Fin (Fintype.card X) â†ª X := hX0.equivFin.symm.toEmbedding
    let b : Fin 2 â†ª Fin (Fintype.card X) := Fin.castLEEmb (by
      simpa [â†Set.toFinset_card, X.ncard_eq_toFinset_card'.symm] using hX2)
    let x0 := a (b âŸ¨0, by simpâŸ©)
    let x1 := a (b âŸ¨1, by simpâŸ©)
    calc
      0 < dist x0 x1 := dist_pos.mpr ((a.injective.comp b.injective).ne (by simp))
      _ â‰¤ diam X := dist_le_diam_of_mem hX1.isBounded x0.2 x1.2
  -- Denote `Y` the points of `X` that lie on the sphere, and let `n` be their number.
  set r := radius X
  have hY0 := Fintype.ofFinite (X âˆ© sphere 0 r : Set V)
  let Y := (X âˆ© sphere 0 r).toFinset
  have hY1 : Y.Nonempty := by simpa [Y, hc] using nonempty_sphere_of_finite hX1 hX3
  have hY2 : Y âŠ† X.toFinset := by simp [Y]
  let n := #Y
  have hn : n â‰  0 := by
    by_contra! hn
    rw [card_eq_zero] at hn
    simp [hn] at hY1
  -- As the center is in the convex hull of `Y`, rewrite it as a convex combination.
  -- `c = âˆ‘ xi âˆˆ Y, l xi â€¢ xi` with `âˆ‘ x i âˆˆ Y, l xi = 1` and `l xi â‰¥ 0`
  have hcY : center X âˆˆ convexHull â„ Y := by
    simpa [Y, hc] using center_mem_convexHull_sphere_of_finite hX1 hX3
  obtain âŸ¨l, hl1, hl2, hl3âŸ© := mem_convexHull'.mp hcY
  -- First, derive a lower bound on `1 - l xi` for `xi âˆˆ Y`.
  have ineq xi (hi : xi âˆˆ Y) := calc
    1 - l xi = âˆ‘ xk âˆˆ Y, l xk - l xi := by rw [hl2]
    _ = âˆ‘ xk âˆˆ Y \ {xi}, l xk + l xi - l xi := by simp [â†sum_sdiff (singleton_subset_iff.mpr hi)]
    _ = âˆ‘ xk âˆˆ Y \ {xi}, l xk * 1 := by ring_nf
    _ â‰¥ âˆ‘ xk âˆˆ Y \ {xi}, l xk * (â€–xk - xiâ€– ^ 2 / diam X ^ 2) := by
      gcongr 2 with xk hk
      Â· simp at hk
        exact hl1 xk hk.left
      Â· simp at hk
        suffices dist xk xi ^ 2 â‰¤ diam X ^ 2 by
          field_simp
          simpa [dist_eq_norm] using this
        gcongr 1
        apply dist_le_diam_of_mem hX1.isBounded
        Â· exact Set.mem_toFinset.mp (hY2 hk.left)
        Â· exact Set.mem_toFinset.mp (hY2 hi)
    _ = (1 / diam X ^ 2) * âˆ‘ xk âˆˆ Y \ {xi}, l xk * â€–xk - xiâ€– ^ 2 := by rw [mul_sum]; field_simp
    _ = (1 / diam X ^ 2) * âˆ‘ xk âˆˆ Y, l xk * â€–xk - xiâ€– ^ 2 := by
      simp [â†sum_sdiff (singleton_subset_iff.mpr hi)]
    _ = (1 / diam X ^ 2) * âˆ‘ xk âˆˆ Y,
          (l xk * â€–xkâ€– ^ 2 + l xk * â€–xiâ€– ^ 2 - 2 * (l xk * âŸªxk, xiâŸ«_â„)) := by
      congr! 2 with xk hk
      rw [norm_sub_sq_real]
      ring
    _ = (1 / diam X ^ 2) *
          (âˆ‘ xk âˆˆ Y, l xk * â€–xkâ€– ^ 2 + âˆ‘ xk âˆˆ Y, l xk * â€–xiâ€– ^ 2 -
          2 * âˆ‘ xk âˆˆ Y, l xk * âŸªxk, xiâŸ«_â„) := by
      congr 1
      conv_lhs => rw [sum_sub_distrib, sum_add_distrib]
      congr 2
      rw [mul_sum]
    _ = (1 / diam X ^ 2) *
          (âˆ‘ xk âˆˆ Y, l xk * r ^ 2 + âˆ‘ xk âˆˆ Y, l xk * r ^ 2 - 2 * âˆ‘ xk âˆˆ Y, l xk * âŸªxk, xiâŸ«_â„) := by
      congr! 6 with xk hk
      Â· simp [Y] at hk
        simp [hk]
      Â· simp [Y] at hi
        simp [hi]
    _ = (1 / diam X ^ 2) *
          (r ^ 2 * âˆ‘ xk âˆˆ Y, l xk + r ^ 2 * âˆ‘ xk âˆˆ Y, l xk - 2 * âˆ‘ xk âˆˆ Y, l xk * âŸªxk, xiâŸ«_â„) := by
      congr 3
      all_goals
      Â· rw [mul_sum]
        congr! 1 with xk hk
        ring
    _ = (1 / diam X ^ 2) * (2 * r ^ 2 - 2 * (âˆ‘ xk âˆˆ Y, l xk * âŸªxk, xiâŸ«_â„)) := by
      congr 2
      rw [hl2]
      ring
    _ = (1 / diam X ^ 2) * (2 * r ^ 2 - 2 * (âˆ‘ xk âˆˆ Y, âŸªl xk â€¢ xk, xiâŸ«_â„)) := by
      congr! 4 with xk hk
      rw [real_inner_smul_left]
    _ = (1 / diam X ^ 2) * (2 * r ^ 2 - 2 * (âŸªâˆ‘ xk âˆˆ Y, l xk â€¢ xk, xiâŸ«_â„)) := by
      congr! 4 with xk hk
      rw [sum_inner]
    _ = (1 / diam X ^ 2) * (2 * r ^ 2) := by simp [hl3, hc]
    _ = 2 * r ^ 2 / diam X ^ 2 := by field_simp
  -- Now, sum this inequality over all `xi âˆˆ Y` to get an inequality involving `n` and `r`.
  replace ineq := calc
    n - 1 = âˆ‘ xi âˆˆ Y, 1 - âˆ‘ i âˆˆ Y, l i := by simp [hl2, n]
    _ = âˆ‘ xi âˆˆ Y, (1 - l xi) := by rw [sum_sub_distrib]
    _ â‰¥ âˆ‘ xi âˆˆ Y, (2 * r ^ 2 / diam X ^ 2) := by gcongr 2 with xi hi; exact ineq xi hi
    _ = n * (2 * r ^ 2 / diam X ^ 2) := by simp [sum_const, n]
    _ = 2 * n * r ^ 2 / diam X ^ 2 := by ring
  -- Rearranging this inequality yields the desired result.
  exact calc
    r = âˆš(r ^ 2) := by
      rw [Real.sqrt_sq]
      calc
        0 â‰¤ _ := dist_nonneg
        _ â‰¤ r := subset hX1.isBounded hX3.choose_spec
    _ â‰¤ âˆš(((n - 1) / (2 * n)) * diam X ^ 2) := by gcongr 1; field_simp at ineq âŠ¢; simpa using ineq
    _ = âˆš((n - 1) / (2 * n)) * âˆš(diam X ^ 2) := by rw [Real.sqrt_mul]; field_simp; simp; omega
    _ = âˆš((n - 1) / (2 * n)) * diam X := by congr 1; apply Real.sqrt_sq; apply diam_nonneg
    _ â‰¤ âˆš(d / (2 * d + 2)) * diam X := by
      gcongr 2
      have := calc
        n â‰¤ #X.toFinset := Finset.card_le_card hY2
        _ = X.ncard := X.ncard_eq_toFinset_card'.symm
        _ â‰¤ d + 1 := hXd
      field_simp
      rify at this
      nlinarith only [this]

/-- Jung's theorem. An upper bound on the radius of the minimal bounding sphere of a bounded set. -/
theorem radius_le_sqrt_of_isBounded [DecidableEq V] (hX1 : IsBounded X) :
    radius X â‰¤ (âˆš(finrank â„ V / (2 * finrank â„ V + 2) : â„) * diam X) := by
  set d := finrank â„ V
  obtain hX2 | hX2 : X.encard â‰¤ d + 1 âˆ¨ X.encard â‰¥ d + 1 := by apply le_total
  Â· apply radius_le_sqrt_of_finite (Set.finite_of_encard_le_coe hX2)
    apply ENat.coe_le_coe.mp
    convert hX2 using 1
    simp [Set.ncard, Set.finite_of_encard_le_coe hX2]
  Â· let f (x : V) := closedBall x (âˆš(d / (2 * d + 2) : â„) * diam X)
    let F (x : X) := f x.val
    suffices (â‹‚ i, F i).Nonempty by
      refine radius_le ?_ hX1 this.choose _ ?_
      Â· apply Set.encard_ne_zero.mp; by_contra! h1; simp [h1] at hX2
      Â· simpa [F, f, mem_closedBall, dist_comm] using this.choose_spec
    apply Convex.helly_theorem_compact (ğ•œ := â„)
    Â· simpa using hX2
    Â· intro âŸ¨i, hiâŸ©
      apply convex_closedBall
    Â· intro âŸ¨i, hiâŸ©
      apply isCompact_closedBall
    Â· intro I hI
      let K := Subtype.val '' SetLike.coe I
      have hK : K.ncard = d + 1 := by
        simpa [K, Set.ncard_image_of_injOn Set.injOn_subtype_val] using hI
      suffices (â‹‚ k âˆˆ K, f k).Nonempty by
        obtain âŸ¨c, hcâŸ© := this
        use c
        simp only [Set.mem_iInter] at hc
        simp only [Set.iInter_coe_set, Set.mem_iInter]
        intro i hi hj
        simpa using hc i âŸ¨âŸ¨i, hiâŸ©, hj, rflâŸ©
      have hK2 : K.Finite := Set.finite_of_ncard_ne_zero (by omega)
      have hK3 : K âŠ† X := by simp [K]
      use center K
      simp only [Set.mem_iInter]
      intro i hi
      have hc := (subset (hX1.subset hK3) hi).trans (radius_le_sqrt_of_finite hK2 hK.le)
      rw [dist_comm] at hc
      apply le_trans hc
      gcongr 1
      exact diam_mono hK3 hX1

end BoundingSphere
