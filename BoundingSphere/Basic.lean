/-
Copyright (c) 2025 Julien Michel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julien Michel
-/
import Mathlib

section
open Bornology ENNReal Metric
variable [PseudoMetricSpace Î±] {X : Set Î±}

/-- The supremal distance from a point `c` to a set `X`,
which is equal to `âŠ¤` if `X` is unbounded. -/
noncomputable def supEDist {Î±} [EDist Î±] (X : Set Î±) c := sSup {edist s c | s âˆˆ X}

/-- If `X` is bounded, then the supremal distance from `X` to `c` is not `âŠ¤`. -/
theorem supEDist_ne_top_of_isBounded (h1 : IsBounded X) c : supEDist X c â‰  âŠ¤ := by
  unfold supEDist
  obtain h2 | h2 := X.eq_empty_or_nonempty
  Â· simp [h2]
  Â· let s0 := h2.choose
    rw [isBounded_iff_ediam_ne_top] at h1
    have := add_ne_top.mpr âŸ¨h1, edist_ne_top s0 câŸ©
    apply ne_top_of_le_ne_top this
    rw [sSup_le_iff]
    intro _ âŸ¨s, hs1, hs2âŸ©
    subst hs2
    apply le_trans (edist_triangle s s0 c)
    gcongr 1
    exact EMetric.edist_le_diam_of_mem hs1 h2.choose_spec

/-- If `X` is unbounded, then the supremal distance from `X` to `c` is `âŠ¤`. -/
theorem supEDist_eq_top_of_not_isBounded (h1 : Â¬IsBounded X) c : supEDist X c = âŠ¤ := by
  unfold supEDist
  contrapose! h1
  rw [isBounded_iff_ediam_ne_top, EMetric.diam_eq_sSup]
  contrapose! h1
  rw [sSup_eq_top] at h1 âŠ¢
  contrapose! h1
  obtain âŸ¨b, hb1, hb2âŸ© := h1
  replace hb2 : âˆ€ s âˆˆ X, edist s c â‰¤ b := by simpa using hb2
  use b + b, add_lt_top.mpr âŸ¨hb1, hb1âŸ©
  intro _ âŸ¨x, hx, y, hy, hxyâŸ©
  subst hxy
  apply le_trans (edist_triangle x c y)
  gcongr 2
  Â· simpa using hb2 x hx
  Â· simpa [edist_comm] using hb2 y hy

/-- If `X` is finite, then the supremal distance from `X` to `c` is attained. -/
theorem supEDist_mem_of_isFinite (h1 : X.Finite) (h2 : X.Nonempty) c :
    supEDist X c âˆˆ (edist Â· c) '' X := by
  have h1' := h1.fintype
  convert_to sSup ((edist Â· c) '' X.toFinset) âˆˆ (edist Â· c) '' X using 1
  Â· rw [Set.coe_toFinset]
    rfl
  rw [â†X.toFinset.sup'_eq_csSup_image (by simpa using h2)]
  apply Finset.sup'_mem
  Â· grind
  Â· intro s hs
    use s, by simpa using hs

/-- If `X` is compact, then the supremal distance from `X` to `c` is attained. -/
theorem supEDist_mem_of_isCompact (h1 : IsCompact X) (h2 : X.Nonempty) c :
    supEDist X c âˆˆ (edist Â· c) '' X := by
  apply IsCompact.sSup_mem
  Â· apply h1.image
    fun_prop
  Â· simp [h2]

theorem edist_le_supEDist c {y} (hy : y âˆˆ X) : edist y c â‰¤ supEDist X c := by
  unfold supEDist
  rw [le_sSup_iff]
  intro b hb
  simp [upperBounds] at hb
  exact hb y hy

theorem supEDist_image_add_right [AddGroup Î±] [IsIsometricVAdd Î±áµƒáµ’áµ– Î±] (X : Set Î±) c a :
    supEDist ((Â· + a) '' X) c = supEDist X (c - a) := by
  apply csSup_eq_csSup_of_forall_exists_le
  Â· intro _ âŸ¨x, hx, hx2âŸ©
    subst hx2
    simp only [Set.mem_setOf_eq, exists_exists_and_eq_and]
    use x - a, by simpa [â†sub_eq_add_neg] using hx, by rw [edist_sub_right]
  Â· intro _ âŸ¨y, hy, hy2âŸ©
    subst hy2
    simp only [Set.mem_setOf_eq, exists_exists_and_eq_and]
    use y + a, by simpa using hy
    calc
      _ = edist (y + a - a) (c - a) := by congr 1; simp
      _ â‰¤ _ := by rw [edist_sub_right]

theorem supEDist_image_sub_right [AddGroup Î±] [IsIsometricVAdd Î±áµƒáµ’áµ– Î±] (X : Set Î±) c a :
    supEDist ((Â· - a) '' X) c = supEDist X (c + a) := by
  convert supEDist_image_add_right X c (-a) using 2
  Â· simp [sub_eq_add_neg]
  Â· simp


/-- The supremal distance from a point `c` to a set `X`, as a real number,
which is equal to `0` if `X` is unbounded. -/
noncomputable def supDist (X : Set Î±) c := (supEDist X c).toReal

theorem supDist_eq c : supDist X c = sSup {dist s c | s âˆˆ X} := by
  unfold supDist supEDist
  rw [toReal_sSup]
  Â· congr 1
    ext x
    simp [edist_dist, dist_nonneg, toReal_ofReal]
  Â· simp [edist_ne_top]

/-- If `X` is unbounded, then the supremal distance from `X` to `c` is `0`. -/
theorem supDist_eq_zero_of_not_isBounded (h1 : Â¬IsBounded X) c : supDist X c = 0 := by
  unfold supDist
  simp [supEDist_eq_top_of_not_isBounded h1, toReal_top]

theorem supEDist_eq_supDist_of_isBounded (h1 : IsBounded X) c :
    supEDist X c = ENNReal.ofReal (supDist X c) := by
  unfold supDist
  rw [ofReal_toReal]
  exact supEDist_ne_top_of_isBounded h1 c

/-- If `X` is finite, then the supremal distance from `X` to `c` is attained. -/
theorem supDist_mem_of_isFinite c (h1 : X.Finite) (h2 : X.Nonempty) :
    supDist X c âˆˆ (dist Â· c) '' X := by
  unfold supDist
  obtain âŸ¨x, hx1, hx2âŸ© := supEDist_mem_of_isFinite h1 h2 c
  rw [â†hx2]
  use x, hx1
  simp [dist_edist]

/-- If `X` is compact, then the supremal distance from `X` to `c` is attained. -/
theorem supDist_mem_of_isCompact (h1 : IsCompact X) (h2 : X.Nonempty) c :
    supDist X c âˆˆ (dist Â· c) '' X := by
  rw [supDist_eq]
  apply IsCompact.sSup_mem
  Â· apply h1.image
    fun_prop
  Â· simp [h2]

theorem dist_le_supDist (h1 : IsBounded X) c {y} (hy : y âˆˆ X) : dist y c â‰¤ supDist X c := by
  unfold supDist
  apply (edist_le_ofReal (by simp)).mp
  change edist y c â‰¤ ENNReal.ofReal (supDist X c)
  rw [â†supEDist_eq_supDist_of_isBounded h1 c]
  apply edist_le_supEDist c hy

theorem supDist_image_add_right [AddGroup Î±] [IsIsometricVAdd Î±áµƒáµ’áµ– Î±] (X : Set Î±) c a :
    supDist ((Â· + a) '' X) c = supDist X (c - a) := by
  unfold supDist
  rw [supEDist_image_add_right]

theorem supDist_image_sub_right [AddGroup Î±] [IsIsometricVAdd Î±áµƒáµ’áµ– Î±] (X : Set Î±) c a :
    supDist ((Â· - a) '' X) c = supDist X (c + a) := by
  unfold supDist
  rw [supEDist_image_sub_right]

end












namespace BoundingSphere
open Bornology ENNReal Metric InnerProductSpace

section
variable {Î±} {X : Set Î±} [PseudoMetricSpace Î±]

/-- The radius of the minimal bounding sphere of a set `X`, defined as the infimum of the supremal
distance from a point to the set. -/
noncomputable def radius (X : Set Î±) := sInf (Set.range (supDist X))

/-- The radius of the minimal bounding sphere is non negative. -/
theorem radius_nonneg : radius X â‰¥ 0 := by
  apply Real.sInf_nonneg ?_
  intro _ âŸ¨x, hxâŸ©
  subst hx
  simp [supDist]

/-- The radius of the minimal bounding sphere of the empty set is `0`. -/
@[simp]
theorem radius_empty [Inhabited Î±] : radius (âˆ… : Set Î±) = 0 := by
  unfold radius supDist supEDist
  simp

theorem ofReal_radius_eq_of_isBounded [Inhabited Î±] (h1 : IsBounded X) :
    ENNReal.ofReal (radius X) = sInf (Set.range (supEDist X)) := by
  unfold radius
  obtain h0 | h0 := X.eq_empty_or_nonempty
  Â· unfold supDist supEDist
    simp [h0]
  symm
  calc
  _ = ENNReal.ofReal (sInf (Set.range (supEDist X))).toReal := by
    rw [ofReal_toReal]
    by_contra! h2
    rw [sInf_eq_top] at h2
    contrapose! h2
    let s0 := h0.choose
    use supEDist X s0, by simp, supEDist_ne_top_of_isBounded h1 s0
  _ = ENNReal.ofReal (sInf (ENNReal.toReal '' Set.range (supEDist X))) := by
    rw [toReal_sInf]
    intro _ âŸ¨x, hxâŸ©
    subst hx
    exact supEDist_ne_top_of_isBounded h1 x
  _ = ENNReal.ofReal (sInf (Set.range (ENNReal.toReal âˆ˜ supEDist X))) := by rw [Set.range_comp]

/-- The radius of the minimal bounding sphere of a bounded set `X` is less than or equal to
that of any other ball containing `X`. -/
theorem radius_le [Inhabited Î±] (h1 : IsBounded X) (h0 : X.Nonempty) :
    âˆ€ c', âˆ€ r', X âŠ† closedBall c' r' â†’ radius X â‰¤ r' := by
  intro c' r' h2
  have hr' := calc
      r' â‰¥ dist h0.choose c' := h2 h0.choose_spec
      _ â‰¥ 0 := dist_nonneg
  rw [â†ofReal_le_ofReal_iff hr', ofReal_radius_eq_of_isBounded h1, sInf_le_iff]
  intro s hs
  replace hs : âˆ€ x, s â‰¤ supEDist X x := by simpa [lowerBounds] using hs
  specialize hs c'
  rw [supEDist, le_sSup_iff] at hs
  apply hs
  intro _ âŸ¨a, ha, ha2âŸ©
  subst ha2
  rw [edist_le_ofReal hr']
  exact h2 ha

/-- The radius of the minimal bounding sphere of a singleton is `0`. -/
@[simp]
theorem radius_singleton [Inhabited Î±] (a : Î±) : radius {a} = 0 := by
  suffices radius {a} â‰¤ 0 by
    apply le_antisymm this
    apply radius_nonneg
  apply radius_le isBounded_singleton (Set.singleton_nonempty a) a 0
  simp

end

section
variable {Î±} {X : Set Î±} [PseudoMetricSpace Î±] [AddGroup Î±] [IsIsometricVAdd Î±áµƒáµ’áµ– Î±]

/-- Translating a set `X` does not change the radius of its minimal bounding sphere. -/
theorem radius_image_add_right (X : Set Î±) a :
    radius ((Â· + a) '' X) = radius X := by
  unfold radius
  convert_to sInf (Set.range (supDist X âˆ˜ (Â· - a))) = _ using 3
  Â· ext c
    rw [supDist_image_add_right, Function.comp_apply]
  congr 1
  apply Function.Surjective.range_comp
  simpa [sub_eq_add_neg] using add_right_surjective (-a)

/-- Translating a set `X` does not change the radius of its minimal bounding sphere. -/
theorem radius_image_sub_right (X : Set Î±) a :
    radius ((Â· - a) '' X) = radius X := by
  simpa [sub_eq_add_neg] using radius_image_add_right X (-a)

end

section
variable {Î±} {X : Set Î±} [PseudoMetricSpace Î±] [Inhabited Î±] [ProperSpace Î±]

/-- If `X` is bounded, then the radius is attained
as the supremal distance from some point in `X`. -/
theorem radius_mem_of_isBounded (h1 : IsBounded X) : radius X âˆˆ Set.range (supDist X) := by
  unfold radius
  obtain h0 | h0 := X.eq_empty_or_nonempty
  Â· unfold supDist supEDist
    simp [h0]

  let s0 := h0.choose
  have hs0 : s0 âˆˆ X := h0.choose_spec
  let K := closedBall s0 (2 * supDist X s0)
  suffices sInf (supDist X '' K) âˆˆ supDist X '' K by
    apply Set.mem_range_of_mem_image (supDist X) K
    convert this using 1
    apply csInf_eq_csInf_of_forall_exists_le
    Â· intro _ âŸ¨c, hcâŸ©
      subst hc
      by_cases hc2 : c âˆˆ K
      Â· use supDist X c
        split_ands
        Â· use c
        Â· simp
      Â· replace hc2 : dist c s0 > 2 * supDist X s0 := by simpa [K] using hc2
        use supDist X s0
        split_ands
        Â· use s0; simp [K, supDist]
        Â· calc
            supDist X c = (supEDist X c).toReal := rfl
            _ â‰¥ (edist s0 c - supEDist X s0).toReal := by
              gcongr 1
              Â· exact supEDist_ne_top_of_isBounded h1 c
              Â· erw [le_sSup_iff]
                intro b hb
                simp [upperBounds] at hb
                calc
                  _ â‰¤ edist s0 c := by apply tsub_le_self
                  _ â‰¤ b := hb s0 hs0
            _ = (edist c s0).toReal - (supEDist X s0).toReal := by
              rw [toReal_sub_of_le]
              Â· rw [edist_comm]
              Â· suffices supDist X s0 â‰¤ dist s0 c by
                  rw [â†toReal_le_toReal (supEDist_ne_top_of_isBounded h1 s0) (edist_ne_top _ _)]
                  rw [edist_dist, toReal_ofReal dist_nonneg]
                  simpa using this
                rw [dist_comm]
                have : supDist X s0 â‰¥ 0 := by unfold supDist; simp
                linarith only [hc2, this]
              Â· apply edist_ne_top
            _ = dist c s0 - supDist X s0 := by congr 1; simp [edist_dist]
            _ â‰¥ _ := by linarith only [hc2]
    Â· intro _ âŸ¨y, hy1, hy2âŸ©
      subst hy2
      use supDist X y
      simp

  apply IsCompact.sInf_mem
  Â· apply IsCompact.image_of_continuousOn
    Â· apply isCompact_closedBall
    Â· apply Continuous.continuousOn
      apply UniformContinuous.continuous
      apply LipschitzWith.uniformContinuous (K := (1 : â„).toNNReal)
      apply LipschitzWith.of_dist_le'
      suffices âˆ€ x y, supDist X x - supDist X y â‰¤ dist x y by
        intro x y
        change |_| â‰¤ _
        rw [abs_le]
        split_ands
        Â· rw [dist_comm]
          linarith only [this y x]
        Â· simpa using this x y
      intro x y
      suffices supDist X x â‰¤ supDist X y + dist x y by linarith only [this]
      calc
        supDist X x = (supEDist X x).toReal := rfl
        _ â‰¤ (supEDist X y + edist x y).toReal := by
          gcongr 1
          Â· exact add_ne_top.mpr âŸ¨supEDist_ne_top_of_isBounded h1 y, by apply edist_ne_topâŸ©
          calc
            supEDist X x = sSup {edist s x | s âˆˆ X} := by rfl
            _ â‰¤ sSup {edist s y | s âˆˆ X} + edist x y := by
              rw [sSup_le_iff]
              intro _ âŸ¨s, hs, hs2âŸ©; subst hs2
              calc
                edist s x â‰¤ edist s y + edist y x := by apply edist_triangle
                _ = edist s y + edist x y := by congr 1; rw [edist_comm]
                _ â‰¤ _ := by
                  gcongr 1
                  rw [le_sSup_iff]
                  intro t ht
                  simp [upperBounds] at ht
                  exact ht s hs
            _ = supEDist X y + edist x y := rfl
        _ = (supEDist X y).toReal + (edist x y).toReal :=
          toReal_add (supEDist_ne_top_of_isBounded h1 y) (by apply edist_ne_top)
        _ = _ := by congr 1; simp [edist_dist]
  Â· use supDist X s0, s0, by simp [K, supDist]

open Classical in
/-- The center of the minimal bounding sphere of a bounded set `X`,
defined as a point where the radius is attained. -/
noncomputable def center (X : Set Î±) :=
  if h1 : IsBounded X then (radius_mem_of_isBounded h1).choose else default

theorem radius_eq_supDist_center_of_isBounded (h1 : IsBounded X) :
    radius X = supDist X (center X) := by
  unfold center
  split_ifs
  exact (radius_mem_of_isBounded h1).choose_spec.symm

/-- The minimal bounding ball of a bounded set `X` contains the set `X`. -/
theorem subset (h1 : IsBounded X) : X âŠ† closedBall (center X) (radius X) := by
  by_cases h0 : X.Nonempty
  Â· intro s hs
    rw [mem_closedBall, radius_eq_supDist_center_of_isBounded h1]
    exact dist_le_supDist h1 (center X) hs
  Â· simp [Set.not_nonempty_iff_eq_empty.mp h0]

end


section
variable {Î±} {X : Set Î±}

/-- A set `X` is minimally enclosed by a closed ball with center `c` and radius `r`
if `X` is contained in the closed ball and any closed ball containing `X` has radius at least
`r`. -/
def IsMinimal [PseudoMetricSpace Î±] (X : Set Î±) c r :=
  X âŠ† closedBall c r âˆ§ âˆ€ c', âˆ€ r', X âŠ† closedBall c' r' â†’ r â‰¤ r'

theorem IsMinimal.of_isBounded [PseudoMetricSpace Î±] [Inhabited Î±] [ProperSpace Î±]
    (h1 : IsBounded X) (h0 : X.Nonempty) :
    IsMinimal X (center X) (radius X) := âŸ¨subset h1, radius_le h1 h0âŸ©


/-- The radius of a minimal bounding sphere is unique. -/
theorem radius_eq_radius_of_IsMinimal [PseudoMetricSpace Î±]
    {x r1 y r2} (h1 : IsMinimal X x r1) (h2 : IsMinimal X y r2) : r1 = r2 :=
  le_antisymm (h1.right y r2 h2.left) (h2.right x r1 h1.left)

/-- The center of a minimal bounding sphere is unique.
Thus the minimal bounding sphere is unique. -/
theorem center_eq_center_of_IsMinimal
    [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±]
    (h0 : X.Nonempty)
    {x r1 y r2} (h1 : IsMinimal X x r1) (h2 : IsMinimal X y r2) : x = y := by
  have h := radius_eq_radius_of_IsMinimal h1 h2
  subst h
  let s0 := h0.choose
  have hs0 : s0 âˆˆ X := h0.choose_spec
  have hr1 := calc
      r1 â‰¥ dist s0 y := h2.left hs0
      _ â‰¥ 0 := dist_nonneg
  let r0 := dist x y / 2
  let c := (1 / 2 : â„) â€¢ (x + y)
  set B1 := closedBall x r1
  set B2 := closedBall y r1
  have h5 z (hz1 : z âˆˆ B1) (hz2 : z âˆˆ B2) : dist z c ^ 2 â‰¤ r1 ^ 2 - r0 ^ 2 := calc
    dist z c ^ 2 = _ := by rw [dist_eq_norm]
    â€–z - câ€– ^ 2 = â€–(1 / 2 : â„) â€¢ (z - x + (z - y))â€– ^ 2 := by
      congr 2
      module
    _ = â€–(1 / 2 : â„)â€– ^ 2 * â€–(z - x + (z - y))â€– ^ 2 := by
      rw [norm_smul]
      ring
    _ = (1 / 4 : â„) * â€–(z - x + (z - y))â€– ^ 2 := by congr 1; norm_num
    _ = (1 / 4 : â„) * (2 * â€–z - xâ€– ^ 2 + 2 * â€–z - yâ€– ^ 2 - â€–x - yâ€– ^ 2) := by
      congr 1
      set a := z - x
      set b := z - y
      convert_to â€–a + bâ€– ^ 2 = 2 * â€–aâ€– ^ 2 + 2 * â€–bâ€– ^ 2 - â€–a - bâ€– ^ 2 using 3
      Â· rw [norm_sub_rev]
        congr 1
        module
      generalize a = a, b = b
      rw [norm_add_sq_real, norm_sub_sq_real]
      ring
    _ = (1 / 2 : â„) * â€–z - xâ€– ^ 2 + (1 / 2 : â„) * â€–z - yâ€– ^ 2 - (1 / 4 : â„) * â€–x - yâ€– ^ 2 := by ring
    _ â‰¤ (1 / 2 : â„) * r1 ^ 2 + (1 / 2 : â„) * r1 ^ 2 - (1 / 4 : â„) * (2 * r0) ^ 2 := by
      gcongr 4
      Â· simpa [B1, dist_eq_norm] using hz1
      Â· simpa [B2, dist_eq_norm] using hz2
      Â· apply le_of_eq
        calc
          _ = dist x y := by ring
          _ = â€–x - yâ€– := by rw [dist_eq_norm]
    _ = r1 ^ 2 - r0 ^ 2 := by ring
  have h6 : X âŠ† closedBall c âˆš(r1 ^ 2 - r0 ^ 2) := by
    intro s hs
    rw [mem_closedBall]
    calc
      _ = âˆš(dist s c ^ 2) := by
        symm
        apply Real.sqrt_sq
        apply dist_nonneg
      _ â‰¤ âˆš(r1 ^ 2 - r0 ^ 2) := Real.sqrt_le_sqrt (h5 s (h1.left hs) (h2.left hs))
  have h3 := h1.right c (âˆš(r1 ^ 2 - r0 ^ 2)) h6
  replace h3 := calc
    r1 ^ 2 â‰¤ âˆš(r1 ^ 2 - r0 ^ 2) ^ 2 := by gcongr 1
    _ = r1 ^ 2 - r0 ^ 2 := by
      apply Real.sq_sqrt
      calc
        0 â‰¤ dist s0 c ^ 2 := by apply sq_nonneg
        _ â‰¤ _ := h5 s0 (h1.left hs0) (h2.left hs0)
  replace h3 : r0 = 0 := by nlinarith only [h3]
  unfold r0 at h3
  replace h3 : dist x y = 0 := by linarith only [h3]
  simpa [dist_eq_zero] using h3

end

section
variable {Î±} {X : Set Î±}
variable [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±]
variable [Inhabited Î±] [ProperSpace Î±]

/-- Translating a bounded set `X` by `a`
translates the center of its minimal bounding sphere by `a`. -/
theorem center_image_add_right (h1 : IsBounded X) (h2 : X.Nonempty) a :
    center ((Â· + a) '' X) = center X + a := by
  set T := ((Â· + a) '' X)
  have h1' : IsBounded T := by
    apply isBounded_image_iff.mpr
    use diam X
    intro x hx y hy
    simpa using dist_le_diam_of_mem h1 hx hy
  have h2' : T.Nonempty := by apply h2.image
  have h3 := IsMinimal.of_isBounded h1' h2'
  have h4 : IsMinimal T (center X + a) (radius X) := by
    split_ands
    Â· simp only [T, Set.image_subset_iff, preimage_add_right_closedBall, add_sub_cancel_right]
      exact subset h1
    Â· intro c' r' h
      simp only [T, Set.image_subset_iff, preimage_add_right_closedBall] at h
      exact radius_le h1 h2 (c' - a) r' h
  exact center_eq_center_of_IsMinimal h2' h3 h4

/-- Translating a bounded set `X` by `-a`
translates the center of its minimal bounding sphere by `-a`. -/
theorem center_image_sub_right (h1 : IsBounded X) (h2 : X.Nonempty) a :
    center ((Â· - a) '' X) = center X - a := by
  simpa [sub_eq_add_neg] using center_image_add_right h1 h2 (-a)

end

section
variable {Î±} {X : Set Î±}

/-- The radius of the minimal bounding sphere of a bounded set `X` with at least two points
is strictly positive. -/
theorem radius_pos [MetricSpace Î±] [Inhabited Î±] [ProperSpace Î±]
    (h1 : IsBounded X) (h2 : X.encard â‰¥ 2) : radius X > 0 := by
  obtain âŸ¨x0, hx0, x1, hx1, h3âŸ© : âˆƒ x0 âˆˆ X, âˆƒ x1 âˆˆ X, x0 â‰  x1 := by
    have f : Fin 2 â†ª X := by
      by_cases h3 : X.Finite
      Â· have := h3.fintype
        let a : Fin (Fintype.card X) â†ª X := this.equivFin.symm.toEmbedding
        let b : Fin 2 â†ª Fin (Fintype.card X) :=
          Fin.castLEEmb (by apply ENat.coe_le_coe.mp; simp [h2])
        exact b.trans a
      Â· let a : â„• â†ª X := Set.Infinite.natEmbedding X h3
        let b : Fin 2 â†ª â„• := Fin.valEmbedding
        exact b.trans a
    let x0 := f âŸ¨0, by simpâŸ©
    let x1 := f âŸ¨1, by simpâŸ©
    use x0.1, x0.2, x1.1, x1.2
    rw [Subtype.coe_inj.ne]
    apply f.injective.ne
    simp
  set r := radius X
  set c := center X
  calc
    r = (r + r) / 2 := by ring
    _ â‰¥ (dist x0 c + dist c x1) / 2 := by
      gcongr 2
      Â· simpa using subset h1 hx0
      Â· simpa [dist_comm] using subset h1 hx1
    _ â‰¥ dist x0 x1 / 2 := by gcongr 1; apply dist_triangle
    _ > 0 / 2 := by gcongr 1; exact dist_pos.mpr h3
    _ = 0 := by simp


/-- The minimal bounding sphere of a finite set `X` hits some point in `X`. -/
theorem nonempty_sphere_of_finite
    [PseudoMetricSpace Î±] [Inhabited Î±] [ProperSpace Î±]
    (h1 : X.Finite) (h2 : X.Nonempty) :
    (X âˆ© sphere (center X) (radius X)).Nonempty := by
  have hc := subset h1.isBounded
  set c := center X
  set r := radius X
  obtain âŸ¨y0, hy0, hy0'âŸ© := supDist_mem_of_isFinite c h1 h2
  dsimp at hy0'
  set r' := supDist X c
  have h3 : r â‰¤ r' := by
    apply radius_le h1.isBounded h2 c r'
    intro s hs
    exact dist_le_supDist h1.isBounded c hs
  have h4 : r' â‰¤ r := by simpa [hy0'] using hc hy0
  replace h2 : r = r' := by linarith only [h3, h4]
  have h5 : y0 âˆˆ X âˆ© sphere c r := by simp [sphere, hy0, hy0', h2]
  use y0

/-- The center of the minimal bounding sphere of a non empty finite set `X`
is contained in the convex hull of the points of `X` that lie on the sphere. -/
theorem center_mem_convexHull_sphere_of_finite
    [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±]
    [Inhabited Î±] [ProperSpace Î±]
    (hX1 : X.Finite) (hX2 : X.Nonempty) :
    center X âˆˆ convexHull â„ (X âˆ© sphere (center X) (radius X)) := by
  set c := center X
  set r := radius X
  -- Denote `Y` the points of `X` that lie on the sphere
  set Y := X âˆ© sphere c r
  have hX3 := hX1.fintype
  have hY1 := Fintype.ofFinite Y
  have hY2 : Y âŠ† X := by simp [Y]
  -- By contradiction, assume that the center `c` is not in the convex hull of `Y`
  by_contra! h1
  -- There exists a vector `v` separating `c` from the convex hull of `Y`
  obtain âŸ¨v, hv, h2âŸ© : âˆƒ v : Î±, v â‰  0 âˆ§ âˆ€ x âˆˆ convexHull â„ Y, âŸªv, x - câŸ«_â„ > 0 := by
    set s : Set Î± := {0}
    have hs1 : Convex â„ s := convex_singleton _
    have hs2 : IsCompact s := isCompact_singleton
    set t := (Â· - c) '' convexHull â„ Y
    have ht1 : Convex â„ t := by
      let f := AffineMap.id â„ _ - AffineMap.const â„ _ c
      apply Convex.affine_image f
      apply convex_convexHull
    have ht2 : IsCompact t := IsCompact.image
      (hX1.subset hY2).isCompact_convexHull (continuous_sub_right c)
    have ht3 : IsClosed t := IsCompact.isClosed ht2
    have ht4 : Y.Nonempty := nonempty_sphere_of_finite hX1 hX2
    have ht5 : t.Nonempty := Set.image_nonempty.mpr ht4.convexHull
    have hst : Disjoint s t := by
      simp [s, t]
      intro x hx
      contrapose! h1
      convert hx using 1
      apply_fun (Â· + c) at h1
      simpa using h1.symm
    obtain âŸ¨f, u, w, hu, huw, hwâŸ© := geometric_hahn_banach_compact_closed hs1 hs2 ht1 ht3 hst
    replace hu : u > 0 := by simpa [s] using hu
    let v := (InnerProductSpace.toDual â„ Î±).symm f
    have hf (x : Î±) : f x = âŸªv, xâŸ«_â„ := by simp [v]
    refine âŸ¨v, ?_, ?_âŸ©
    Â· by_contra! hv
      specialize hw ht5.choose ht5.choose_spec
      simp [hf, hv] at hw
      linarith only [hu, huw, hw]
    Â· intro x hx
      specialize hw (x - c) (by simp [t, hx])
      simp [hf] at hw
      linarith only [hu, huw, hw]
  -- Perturb the center `c` a bit in the direction of `v`
  let c' (Îµ : â„) := c + Îµ â€¢ v
  -- For a small enough perturbation, all points of `Y` are in the interior of the ball
  obtain âŸ¨Î´Y, hÎ´Y, hcYâŸ© : âˆƒ Î´Y > 0, âˆ€ Îµ, Îµ > 0 â†’ Îµ < Î´Y â†’ âˆ€ x âˆˆ Y, â€–x - c' Îµâ€– ^ 2 < r ^ 2 := by
    let Î´ x := âŸªv, x - câŸ«_â„
    have hY3 : Y.toFinset.Nonempty := Set.toFinset_nonempty.mpr (nonempty_sphere_of_finite hX1 hX2)
    let d := Y.toFinset.inf' hY3 Î´
    have hd1 xi (hxi : xi âˆˆ Y) : d â‰¤ Î´ xi := Y.toFinset.inf'_le Î´ (by simpa using hxi)
    have hd2 : âˆƒ xi âˆˆ Y, Î´ xi = d := by
      convert Y.toFinset.exists_mem_eq_inf' hY3 Î´ using 2 with xi; simp [d]; tauto
    have hd3 : d > 0 := by
      obtain âŸ¨x0, hx0, hdâŸ© := hd2
      rw [â†hd]
      apply h2 x0
      exact mem_convexHull_iff.mpr fun _ a _ => a hx0
    use 2 * d / â€–vâ€– ^ 2, by field_simp; nlinarith only [hd3]
    intro Îµ hÎµ1 hÎµ2 xi hxi
    calc
      â€–xi - c' Îµâ€– ^ 2 = â€–(xi - c) - Îµ â€¢ vâ€– ^ 2 := by congr 2; module
      _ = â€–xi - câ€– ^ 2 - 2 * Îµ * âŸªv, xi - câŸ«_â„ + â€–Îµ â€¢ vâ€– ^ 2 := by
        rw [norm_sub_sq_real, real_inner_comm, real_inner_smul_left]
        ring
      _ = â€–xi - câ€– ^ 2 - 2 * Îµ * âŸªv, xi - câŸ«_â„ + Îµ ^ 2 * â€–vâ€– ^ 2 := by
        congr 1
        rw [norm_smul, mul_pow, Real.norm_of_nonneg]
        exact hÎµ1.le
      _ â‰¤ â€–xi - câ€– ^ 2 - 2 * Îµ * d + Îµ ^ 2 * â€–vâ€– ^ 2 := by gcongr 3; exact hd1 xi hxi
      _ = â€–xi - câ€– ^ 2 + (-2 * Îµ * d + Îµ ^ 2 * â€–vâ€– ^ 2) := by ring
      _ < â€–xi - câ€– ^ 2 + 0 := by
        gcongr 1
        calc
          -2 * Îµ * d + Îµ ^ 2 * â€–vâ€– ^ 2 = Îµ * (-2 * d + Îµ * â€–vâ€– ^ 2) := by ring
          _ < Îµ * 0 := by
            gcongr 1
            calc
              _ < -2 * d + (2 * d / â€–vâ€– ^ 2) * â€–vâ€– ^ 2 := by gcongr 2
              _ = -2 * d + 2 * d := by congr 1; field_simp
              _ = _ := by ring
          _ = _ := by ring
      _ = _ := by simp [Y] at hxi; simp [hxi]
  -- For a small enough perturbation, all points of `X \ Y` are also in the interior of the ball
  let Z := X \ Y
  obtain âŸ¨Î´Z, hÎ´Z, hcZâŸ© : âˆƒ Î´Z > 0, âˆ€ Îµ, Îµ > 0 â†’ Îµ < Î´Z â†’ âˆ€ x âˆˆ Z, â€–x - c' Îµâ€– ^ 2 < r ^ 2 := by
    have hZ0 := Fintype.ofFinite Z
    by_cases hZ1 : Z = âˆ…
    Â· simp [hZ1]; use 1; norm_num
    replace hZ1 := Set.toFinset_nonempty.mpr (Set.nonempty_iff_ne_empty.mpr hZ1)
    let f Îµ := Z.toFinset.sup' hZ1 (fun x => â€–x - c' Îµâ€– ^ 2)
    have hf : Continuous f := by apply Continuous.finset_sup'_apply; fun_prop
    replace hf : ContinuousAt f 0 := by apply hf.continuousAt
    rw [Metric.continuousAt_iff] at hf
    have f0_lt : f 0 < r ^ 2 := by
      unfold f
      rw [Finset.sup'_lt_iff]
      intro x hx
      suffices dist x c ^ 2 < r ^ 2 by simpa [c', â†dist_eq_norm] using this
      rw [sq_lt_sqâ‚€]
      Â· simp [Z] at hx
        apply lt_of_le_of_ne
        Â· exact subset hX1.isBounded hx.left
        Â· have := hx.right
          contrapose! this
          simp [Y, hx.left, â†dist_eq_norm, this]
      Â· apply dist_nonneg
      Â· apply radius_nonneg
    replace âŸ¨Î´, hÎ´, hfâŸ© := hf (r ^ 2 - f 0) (by linarith only [f0_lt])
    use Î´, hÎ´
    intro Îµ hÎµ1 hÎµ2
    simp only [dist_eq_norm] at hf
    have hÎµÎ´ : â€–Îµ - 0â€– < Î´ := by
      rw [Real.norm_of_nonneg]
      Â· linarith only [hÎµ2]
      Â· linarith only [hÎµ1]
    specialize hf hÎµÎ´
    intro x hx
    calc
      _ â‰¤ f Îµ := by
        unfold f
        rw [Finset.le_sup'_iff]
        use x, by simpa using hx
      _ = (f Îµ - f 0) + f 0 := by ring
      _ â‰¤ â€–f Îµ - f 0â€– + f 0 := by gcongr 1; apply Real.le_norm_self
      _ < r ^ 2 := by linarith only [hf]
  -- Thus perturbing the center by a small amout yields a smaller ball still enclosing all of `X`,
  obtain âŸ¨Î´X, hÎ´X, hcXâŸ© : âˆƒ Î´X > 0, âˆ€ Îµ, Îµ > 0 â†’ Îµ < Î´X â†’ âˆ€ x âˆˆ X, â€–x - c' Îµâ€– ^ 2 < r ^ 2 := by
    use Î´Y âŠ“ Î´Z, lt_min hÎ´Y hÎ´Z
    intro Îµ hÎµ1 hÎµ2 x hx
    obtain h | h : x âˆˆ Y âˆ¨ x âˆˆ Z := by simp [Y, Z, hx]; tauto
    Â· exact hcY Îµ hÎµ1 (lt_of_lt_of_le hÎµ2 inf_le_left) x h
    Â· exact hcZ Îµ hÎµ1 (lt_of_lt_of_le hÎµ2 inf_le_right) x h
  -- Contradicting the minimality of the original ball.
  let Î´0 := Î´X / 2
  obtain âŸ¨x, hx, hr0âŸ© := X.toFinset.exists_mem_eq_sup' (Set.toFinset_nonempty.mpr hX2) (â€–Â· - c' Î´0â€–)
  set r0 := X.toFinset.sup' (Set.toFinset_nonempty.mpr hX2) (â€–Â· - c' Î´0â€–)
  have h3 : X âŠ† closedBall (c' Î´0) r0 := by
    intro x hx
    simp only [mem_closedBall, dist_eq_norm, r0]
    rw [Finset.le_sup'_iff]
    use x, by simpa using hx
  have h4 : r â‰¤ r0 := radius_le hX1.isBounded hX2 (c' Î´0) r0 h3
  have h5 := calc
    r0 = âˆš(r0 ^ 2) := by
      rw [Real.sqrt_sq]
      unfold r0
      rw [Finset.le_sup'_iff]
      use hX2.choose, by simpa using hX2.choose_spec
      apply norm_nonneg
    _ < âˆš(r ^ 2) := by
      apply Real.sqrt_lt_sqrt
      Â· apply sq_nonneg
      rw [hr0]
      apply hcX Î´0
      Â· unfold Î´0; linarith only [hÎ´X]
      Â· unfold Î´0; linarith only [hÎ´X]
      Â· simpa using hx
    _ = r := by
      rw [Real.sqrt_sq]
      apply radius_nonneg
  linarith only [h4, h5]

/-- A finite set with at least two points has at least two points on the boundary
of its minimal bounding sphere. -/
theorem encard_sphere_ge_two_of_finite
    [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±]
    [Inhabited Î±] [ProperSpace Î±]
    (h : X.encard â‰¥ 2) (h' : X.Finite) :
    (X âˆ© sphere (center X) (radius X)).encard â‰¥ 2 := by
  have hX : IsBounded X := h'.isBounded
  have hX2 : X.Nonempty := by
    apply Set.encard_ne_zero.mp
    by_contra! h0
    simp [h0] at h
  have hr := radius_le hX hX2
  have hc := subset hX
  set c := center X
  set r := radius X
  let hit := X âˆ© sphere (center X) (radius X)
  change hit.encard â‰¥ 2
  obtain h0 | h0 : Â¬hit.Finite âˆ¨ hit.Finite := by tauto
  Â· rw [Set.encard_eq_top]
    Â· simp
    Â· simpa using h0
  obtain h1 | h1 | h1 : hit.encard = 0 âˆ¨ hit.encard = 1 âˆ¨ hit.encard â‰¥ 2 := by
    have := h0.fintype
    unfold Set.encard
    rw [ENat.card_eq_coe_natCard]
    norm_cast
    omega
  Â· exfalso
    rw [Set.encard_eq_zero] at h1
    have h2 := nonempty_sphere_of_finite h' hX2
    contrapose! h2
    exact h1
  Â· exfalso
    rw [Set.encard_eq_one] at h1
    obtain âŸ¨x, hxâŸ© := h1
    have hx1 : x âˆˆ hit := by simp [hx]
    have hx2 : x âˆˆ X := hx1.left
    have hx3 := hx1.right
    have h1 : c âˆˆ convexHull â„ hit := center_mem_convexHull_sphere_of_finite h' hX2
    replace h1 : c = x := by simpa [hx] using h1
    have h2 : r = 0 := by simpa [sphere, c, h1] using hx3.symm
    have h3 : r > 0 := radius_pos hX h
    linarith only [h2, h3]
  Â· exact h1


open Finset in
/-- An upper bound on the radius of the minimal bounding sphere of a finite set `X` -/
theorem radius_le_sqrt_of_finite
    [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±] [Inhabited Î±] [ProperSpace Î±] [DecidableEq Î±]
    (hX2 : X.Finite) :
    let d := X.ncard - 1
    radius X â‰¤ âˆš(d / (2 * d + 2) : â„) * diam X := by

  intro d
  obtain hX3 | hX3 | hX3 : X.ncard = 0 âˆ¨ X.ncard = 1 âˆ¨ X.ncard â‰¥ 2 := by omega
  Â· rw [Set.ncard_eq_zero hX2] at hX3
    simp [hX3]
  Â· have âŸ¨a, haâŸ© := Set.ncard_eq_one.mp hX3
    simp [ha, radius_singleton]

  have hX4 : X.Nonempty := by
    by_contra! h
    rw [â†Set.ncard_eq_zero (Set.finite_of_ncard_ne_zero (by linarith only [hX3]))] at h
    omega

  wlog hc : center X = 0
  Â· let T := (Â· - center X) '' X
    have hT : T.ncard = X.ncard := Set.ncard_image_of_injective _ sub_left_injective
    specialize this (X := T)
    specialize this (Set.Finite.image (Â· - center X) hX2)
    specialize this (by simpa [hT] using hX3)
    specialize this (by simpa [T] using hX4)
    specialize this (by simp [T, center_image_sub_right hX2.isBounded hX4])
    convert this using 1
    Â· simp [T, radius_image_sub_right]
    Â· congr 1
      Â· rw [hT]
      Â· unfold diam
        congr 1
        iterate 2 rw [EMetric.diam_eq_sSup]
        congr 1
        ext x
        simp [T]

  have hX : IsBounded X := hX2.isBounded
  set r := radius X
  let h3 := subset hX

  have h1' := hX2.fintype
  have h1 : X.toFinset.card â‰¥ 2 := by
    convert hX3 using 1
    exact Eq.symm (Set.ncard_eq_toFinset_card' X)

  have := Fintype.ofFinite (X âˆ© sphere 0 r : Set Î±)
  let X' : Finset Î± := (X âˆ© sphere 0 r : Set Î±).toFinset
  have hX'1 : X'.Nonempty := by simpa [X', hc] using nonempty_sphere_of_finite h1'.finite hX4
  have hS' : X' âŠ† X.toFinset := by simp [X']

  let n := #X'
  have hn : n â‰  0 := by
    by_contra! hn
    rw [card_eq_zero] at hn
    simp [hn] at hX'1

  have h5 : center X âˆˆ convexHull â„ X' := by
    simpa [X', hc] using center_mem_convexHull_sphere_of_finite hX2 hX4

  obtain âŸ¨l, h6, h7, h8âŸ© : âˆƒ (l : Î± â†’ â„),
      (âˆ€ k âˆˆ X', l k â‰¥ 0) âˆ§ âˆ‘ x âˆˆ X', l x = 1 âˆ§ center X = âˆ‘ x âˆˆ X', l x â€¢ x := by
    rw [mem_convexHull'] at h5
    obtain âŸ¨w, g1, g2, g3âŸ© := h5
    use w
    exact âŸ¨fun k hk => g1 k (by simpa using hk), g2, g3.symmâŸ©

  have h8' : diam X > 0 := by
    let a : Fin (Fintype.card X) â†ª X := h1'.equivFin.symm.toEmbedding
    let b : Fin 2 â†ª Fin (Fintype.card X) := Fin.castLEEmb (by simpa [â†Set.toFinset_card] using h1)
    let x0 := a (b âŸ¨0, by simpâŸ©)
    let x1 := a (b âŸ¨1, by simpâŸ©)
    have x : x0 â‰  x1 := (a.injective.comp b.injective).ne (by simp)
    calc
      0 < dist x0 x1 := by apply dist_pos.mpr; exact x
      _ â‰¤ diam X := dist_le_diam_of_mem hX x0.2 x1.2

  have h9 (i : Î±) (hi : i âˆˆ X') := calc
    1 - l i = âˆ‘ k âˆˆ X', l k - l i := by rw [h7]
    _ = âˆ‘ k âˆˆ X' \ {i}, l k + l i - l i := by
      have h : {i} âŠ† X' := by simpa using hi
      simp [â†sum_sdiff h]
    _ = âˆ‘ k âˆˆ X' \ {i}, l k * 1 := by ring_nf
    _ â‰¥ âˆ‘ k âˆˆ X' \ {i}, l k * (â€–k - iâ€– ^ 2 / diam X ^ 2) := by
      gcongr 2 with k hk
      Â· simp at hk
        exact h6 k (by simp [hk])
      Â· simp at hk
        suffices dist k i ^ 2 â‰¤ diam X ^ 2 by
          field_simp
          simpa [dist_eq_norm] using this
        gcongr 1
        apply dist_le_diam_of_mem hX
        Â· exact Set.mem_toFinset.mp (hS' hk.left)
        Â· exact Set.mem_toFinset.mp (hS' hi)
    _ = (1 / diam X ^ 2) * âˆ‘ k âˆˆ X' \ {i}, l k * â€–k - iâ€– ^ 2 := by
      rw [mul_sum]
      congr! 1 with k hk
      field_simp
    _ = (1 / diam X ^ 2) * âˆ‘ k âˆˆ X', l k * â€–k - iâ€– ^ 2 := by
      congr 1
      have h : {i} âŠ† X' := by simpa using hi
      simp [â†sum_sdiff h]
    _ = (1 / diam X ^ 2) * âˆ‘ k âˆˆ X',
          (l k * â€–kâ€– ^ 2 + l k * â€–iâ€– ^ 2 - 2 * (l k * âŸªk, iâŸ«_â„)) := by
      congr! 2 with k hk
      rw [norm_sub_sq_real]
      ring
    _ = (1 / diam X ^ 2) * (
          âˆ‘ k âˆˆ X', l k * â€–kâ€– ^ 2 + âˆ‘ k âˆˆ X', l k * â€–iâ€– ^ 2 -
          2 * âˆ‘ k âˆˆ X', l k * âŸªk, iâŸ«_â„) := by
      congr 1
      conv_lhs => rw [sum_sub_distrib, sum_add_distrib]
      congr 2
      rw [mul_sum]
    _ = (1 / diam X ^ 2) * (
          âˆ‘ k âˆˆ X', l k * r ^ 2 + âˆ‘ k âˆˆ X', l k * r ^ 2 -
          2 * âˆ‘ k âˆˆ X', l k * âŸªk, iâŸ«_â„) := by
      congr! 6 with k hk
      Â· simp [X'] at hk
        simp [hk]
      Â· simp [X'] at hi
        simp [hi]
    _ = (1 / diam X ^ 2) * (
          r ^ 2 * âˆ‘ k âˆˆ X', l k + r ^ 2 * âˆ‘ k âˆˆ X', l k -
          2 * âˆ‘ k âˆˆ X', l k * âŸªk, iâŸ«_â„) := by
      congr 3
      all_goals
      Â· rw [mul_sum]
        congr! 1 with k hk
        ring
    _ = (1 / diam X ^ 2) * (2 * r ^ 2 - 2 * âˆ‘ k âˆˆ X', l k * âŸªk, iâŸ«_â„) := by
      congr 2
      rw [h7]
      ring
    _ = (1 / diam X ^ 2) * (2 * r ^ 2 - 2 * (âˆ‘ k âˆˆ X', l k * âŸªk, iâŸ«_â„)) := by
      ring
    _ = (1 / diam X ^ 2) * (2 * r ^ 2 - 2 * (âˆ‘ k âˆˆ X', âŸªl k â€¢ k, iâŸ«_â„)) := by
      congr! 4 with k hk
      rw [real_inner_smul_left]
    _ = (1 / diam X ^ 2) * (2 * r ^ 2 - 2 * (âŸªâˆ‘ k âˆˆ X', l k â€¢ k, iâŸ«_â„)) := by
      congr! 4 with k hk
      rw [sum_inner]
    _ = (1 / diam X ^ 2) * (2 * r ^ 2) := by simp [â†h8, hc]
    _ = 2 * r ^ 2 / diam X ^ 2 := by field_simp

  have h10 := calc
    n - 1 = âˆ‘ i âˆˆ X', 1 - âˆ‘ i âˆˆ X', l i := by simp [h7, n]
    _ = âˆ‘ i âˆˆ X', (1 - l i) := by rw [sum_sub_distrib]
    _ â‰¥ âˆ‘ i âˆˆ X', (2 * r ^ 2 / diam X ^ 2) := by
      gcongr 2 with i hi
      exact h9 i hi
    _ = n * (2 * r ^ 2 / diam X ^ 2) := by simp [sum_const, n]
    _ = 2 * n * r ^ 2 / diam X ^ 2 := by ring

  exact calc
    r = âˆš(r ^ 2) := by
      symm
      apply Real.sqrt_sq
      calc
        0 â‰¤ _ := by apply dist_nonneg
        _ â‰¤ r := h3 hX4.choose_spec
    _ â‰¤ âˆš(((n - 1) / (2 * n)) * diam X ^ 2) := by
      apply Real.sqrt_le_sqrt
      field_simp at h10 âŠ¢
      simpa using h10
    _ = âˆš((n - 1) / (2 * n)) * âˆš(diam X ^ 2) := by
      rw [Real.sqrt_mul]
      field_simp
      simp
      omega
    _ = âˆš((n - 1) / (2 * n)) * diam X := by
      congr 1
      apply Real.sqrt_sq
      apply diam_nonneg
    _ â‰¤ âˆš(d / (2 * d + 2)) * diam X := by
      gcongr 2
      field_simp
      have : n â‰¤ d + 1 := calc
        #X' â‰¤ #X.toFinset := Finset.card_le_card hS'
        _ = X.ncard := Eq.symm (Set.ncard_eq_toFinset_card' X)
        _ = d + 1 := by omega
      rify at this
      nlinarith only [this]

open Finset in
/-- An upper bound on the radius of the minimal bounding sphere of a bounded set `X`
with cardinality greater than the dimension of the ambient space -/
theorem radius_le_sqrt_of_encard_gt_finrank
    [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±]
    [Inhabited Î±] [ProperSpace Î±] [DecidableEq Î±]
    [FiniteDimensional â„ Î±]
    (hX : IsBounded X) (hX2 : X.encard â‰¥ Module.finrank â„ Î± + 1) :
    let d := Module.finrank â„ Î±
    radius X â‰¤ (âˆš(d / (2 * d + 2) : â„) * diam X) := by
  intro d

  have hX3 : X.Nonempty := by
    apply Set.encard_ne_zero.mp
    by_contra! h1
    simp [h1] at hX2

  suffices âˆƒ c, X âŠ† closedBall c (âˆš(d / (2 * d + 2) : â„) * diam X) by
    obtain âŸ¨c, hcâŸ© := this
    apply radius_le hX hX3 c _ hc

  let F (x : X) := closedBall x.val (âˆš(d / (2 * d + 2) : â„) * diam X)

  suffices (â‹‚ i, F i).Nonempty by
    let c := this.choose
    have hc : c âˆˆ (â‹‚ y : X, F y) := this.choose_spec
    simp [F] at hc
    use c
    simpa [mem_closedBall, dist_comm] using hc

  apply Convex.helly_theorem_compact (ğ•œ := â„)
  Â· simpa using hX2
  Â· intro âŸ¨i, hiâŸ©
    apply convex_closedBall
  Â· intro âŸ¨i, hiâŸ©
    apply isCompact_closedBall
  Â· intro I hI
    let I' := SetLike.coe I
    replace hI : #I = d + 1 := by simpa using hI
    simp only [Set.iInter_coe_set, Set.nonempty_iInter, Set.mem_iInter]
    set c := center (Subtype.val '' I')
    have hI' := calc
      (Subtype.val '' I').ncard = I'.ncard := Set.ncard_image_of_injOn Set.injOn_subtype_val
      _ = d + 1 := by simpa [I'] using hI
    have hc : radius (Subtype.val '' I') â‰¤ _ := radius_le_sqrt_of_finite (Set.toFinite _)
    rw [hI'] at hc
    have hc' := subset (IsBounded.subset hX (Subtype.coe_image_subset X I))
    rw [Set.image_subset_iff] at hc'
    use c
    intro i hi hi2
    specialize hc' hi2
    suffices dist c i â‰¤ âˆš(d / (2 * d + 2) : â„) * diam (X) by simpa [F] using this
    replace hc : dist c i â‰¤ âˆš(d / (2 * d + 2) : â„) * diam (Subtype.val '' I') := by
      simpa [dist_comm] using hc'.trans hc
    apply le_trans hc
    gcongr 1
    exact diam_mono (Subtype.coe_image_subset X I) hX

/-- An upper bound on the radius of the minimal bounding sphere of a bounded set `X` -/
theorem radius_le_sqrt_of_isBounded
    [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±]
    [Inhabited Î±] [ProperSpace Î±] [DecidableEq Î±]
    [FiniteDimensional â„ Î±]
    (hX : IsBounded X) :
    let d := Module.finrank â„ Î±
    radius X â‰¤ (âˆš(d / (2 * d + 2) : â„) * diam X) := by
  intro d
  obtain h2 | h2 : X.encard â‰¤ d + 1 âˆ¨ X.encard â‰¥ d + 1 := by apply le_total
  Â· apply le_trans (radius_le_sqrt_of_finite (Set.finite_of_encard_le_coe h2))
    gcongr 2
    replace h2 : X.ncard â‰¤ d + 1 := by
      apply ENat.coe_le_coe.mp
      convert h2 using 1
      simp [Set.ncard, Set.finite_of_encard_le_coe h2]
    replace h2 : X.ncard - 1 â‰¤ d := by omega
    rify at h2
    field_simp
    nlinarith only [h2]
  Â· exact radius_le_sqrt_of_encard_gt_finrank hX h2

/-- Jung's theorem. A bounded set `X` is contained in a closed ball of radius
at most `âˆš(d / (2 * d + 2)) * diam X`, where `d` is the dimension of the ambient space. -/
theorem jung_theorem
    [NormedAddCommGroup Î±] [InnerProductSpace â„ Î±]
    [Inhabited Î±] [ProperSpace Î±] [DecidableEq Î±]
    [FiniteDimensional â„ Î±]
    (hX : IsBounded X) :
    let d := Module.finrank â„ Î±
    âˆƒ c, X âŠ† closedBall c (âˆš(d / (2 * d + 2) : â„) * diam X) := by
  use center X
  apply (subset hX).trans
  apply closedBall_subset_closedBall
  exact radius_le_sqrt_of_isBounded hX

end

end BoundingSphere
